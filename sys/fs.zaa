//
// sys fs
//

import std.stdio;
import std.string : String;
import std.vector;
import std.arc;
import sys.bits.fs : getcwd, stat, lstat, filetype, dirp;

pub fn stat(String &path) throws -> stat
{
  var stat = stat();

  stat(sys::path_view(path), &mut stat);

  return stat;
}

pub fn lstat(String &path) throws -> stat
{
  var stat = stat();

  lstat(sys::path_view(path), &mut stat);

  return stat;
}

pub fn exists(String &path) throws -> bool
{
  try
  {
    return lstat(path).type != filetype::unknown;
  }
  catch (std::error err)
  {
    if (err == std::errc::not_found)
      return false;

    throw err;
  }
}

pub fn cwd() throws -> std::string
{
  var path = std::string();

  getcwd(&mut path);

  return path;
}

pub struct directory_view
{
  std::arc<dirp> state;

  pub struct directory_iterator
  {
    std::arc<dirp> state;

    directory_iterator(std::arc<dirp> state)
      : state(&move state)
    {
    }

    pub directory_iterator() = default;
    pub directory_iterator(directory_iterator &&) = default;
    pub fn =(directory_iterator mut &, directory_iterator &&) -> directory_iterator mut & = default;
    pub ~directory_iterator() = default;

    #[lifetime(poison(this))]
    pub fn ++(directory_iterator mut &this) throws -> directory_iterator mut &
    {
      readdir(&mut *this.state);

      return &this;
    }

    pub fn ==(directory_iterator &lhs, directory_sentinal &rhs) -> bool
    {
      return lhs.state.eof;
    }
  }

  pub struct recursive_directory_iterator
  {
    bool descend;
    std::arc<std::vector<std::arc<dirp>>> stack;

    pub fn depth(this &) -> usize
    {
      return this.stack.len;
    }

    pub fn descend(this mut &) -> void
    {
      this.descend = true;
    }

    recursive_directory_iterator(std::arc<dirp> state)
    {
      if (!state.eof)
        this.stack.push_back(state);
    }

    pub recursive_directory_iterator() = default;
    pub recursive_directory_iterator(recursive_directory_iterator &&) = default;
    pub fn =(recursive_directory_iterator mut &, recursive_directory_iterator &&) -> recursive_directory_iterator mut & = default;
    pub ~recursive_directory_iterator() = default;

    #[lifetime(poison(this))]
    pub fn ++(recursive_directory_iterator mut &this) throws -> recursive_directory_iterator mut &
    {
      if (this.descend)
      {
        this.stack.push_back();

        opendir(&mut *this.stack.back, this.stack[this.stack.len - 2].path);

        this.descend = false;

        if (!this.stack.back.eof)
          return &this;

        this.stack.pop_back();
      }

      while (this.stack.len != 0)
      {
        readdir(&mut *this.stack.back);

        if (!this.stack.back.eof)
          break;

        this.stack.pop_back();
      }

      return &this;
    }

    pub fn ==(recursive_directory_iterator &lhs, directory_sentinal &rhs) -> bool
    {
      return lhs.stack.empty;
    }
  }

  pub struct directory_sentinal
  {
    pub directory_sentinal() = default;
    pub directory_sentinal(directory_sentinal &&) = default;
    pub fn =(directory_sentinal mut &lhs, directory_sentinal &&rhs) -> directory_sentinal mut & = default;
    pub ~directory_sentinal() = default;
  }

  #[lifetime(depend(*this))]
  pub fn begin(this &) throws -> directory_iterator
  {
    return directory_iterator(this.state);
  }

  #[lifetime(depend(*this))]
  pub fn descend(this &) throws -> recursive_directory_iterator
  {
    return recursive_directory_iterator(this.state);
  }

  pub fn end(this &) -> directory_sentinal
  {
    return directory_sentinal();
  }

  #[lifetime(repose(iter, this), depend(iter))]
  pub fn [](this &, directory_iterator &iter)
  {
    return &*iter.state;
  }

  #[lifetime(repose(iter, this), depend(iter))]
  pub fn [](this &, recursive_directory_iterator &iter)
  {
    return &*iter.stack.back;
  }

  directory_view(std::arc<dirp> &&state)
    : state(&&state)
  {
  }

  pub directory_view(directory_view &&) = default;
  pub ~directory_view() = default;
}

pub fn read_dir(String &path) throws -> directory_view
{
  var state = std::arc<dirp>();

  opendir(&mut *state, sys::path_view(path));

  return directory_view(&move state);
}

pub fn fmt(std::OutputStream mut &os, std::fmt_context mut &ctx, directory_view &dir) throws -> void
{
  for (var &entry : dir)
    std::print_to(&mut os, entry, " ");
}
