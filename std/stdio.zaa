//
// std stdio
//

import std.io;
import std.string;
import std.sys.fd;
import std.fmt;

using std::fd;
using std::String;

pub struct file
{
  pub fn create(String &path) throws -> file
  {
    var fd = fd::open(path, fd::oflags::create | fd::oflags::trunc, fd::rights::read | fd::rights::write);

    return file(fd);
  }

  fn read(this mut &, u8 mut *data, usize len) throws -> usize
  {
    return this.fd.read(data, len);
  }

  fn write(this mut &, u8 *data, usize len) throws -> usize
  {
    return this.fd.write(data, len);
  }

  fn print(this mut &, var & ...args) throws -> usize
  {
    return std::print_to(this, args..., '\n');
  }

  fn printf(this mut &, String &format, var & ...args) throws -> usize
  {
    return std::format_to(this, format, args...);
  }
  
  fn printf(this mut &, #std::string_literal format, var & ...args) throws -> usize
  {
    return std::format_to(this, format, args...);
  }

  file(fd fd)
    : fd(fd)
  {
  }

  ~file()
  {
    fd.close();
  }

  std::fd fd;
}

pub struct StdIn
{
  fn read(this mut &, u8 mut *data, usize len) -> usize
  {
    try
    {
      return fd(STDIN).read(data, len);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  StdIn() = default;
  StdIn(StdIn&) = default;
  ~StdIn() = default;
}

pub fn stdin_raw -> file
{
  return file(fd(STDIN));
}

pub fn stdin -> StdIn mut &
{
  static instance = #StdIn();

  return &instance;
}

pub struct StdOut
{
  fn write(this mut &, u8 *data, usize len) -> usize
  {
    try
    {
      return fd(STDOUT).write(data, len);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  fn putc(this mut &, u8 ch) -> usize
  {
    try
    {
      return write(this, &ch, 1);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  fn print(this mut &, var & ...args) -> usize
  {
    try
    {
      return std::print_to(this, args..., '\n');
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  fn printf(this mut &, String &format, var & ...args) -> usize
  {
    try
    {
      return std::format_to(this, format, args...);
    }
    catch(std::error e)
    {
      return 0;
    }
  }
  
  fn printf(this mut &, #std::string_literal format, var & ...args) -> usize
  {
    try
    {
      return std::format_to(this, format, args...);
    }
    catch(std::error e)
    {
      return 0;
    }
  }
  
  fn flush(this mut &) -> void
  {
  }
  
  StdOut() = default;
  StdOut(StdOut&) = default;
  ~StdOut() = default;
}

pub fn stdout_raw -> file
{
  return file(fd(STDOUT));
}

pub fn stdout -> StdOut mut &
{
  static instance = #StdOut();

  return &instance;
}

pub fn print(var & ...args) -> usize
{
  return stdout.print(args...);
}

pub fn printf(String &format, var & ...args) -> usize
{
  return stdout.printf(format, args...);
}

pub fn printf(#std::string_literal format, var & ...args) -> usize
{
  return stdout.printf(format, args...);
}
