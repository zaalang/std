//
// std list
//

import std.memory;
import std.stdlib : assert;
import std.algorithm : swap;
import std.range : Sentinal, Range;
import std.type : as_const;

pub struct list<T, Allocator = std::allocator>
{
  pub list(Allocator &allocator = Allocator())
    : _allocator(allocator)
  {
    init(this);
  }

  pub list(list &&other, Allocator &allocator = Allocator())
    : _allocator(allocator)
  {
    init(this);

    insert(this, this.end, &&other);
  }

  pub fn =(this mut &, list &&other) -> list mut &
  {
    if (&this == &other)
      return &this;

    clear(this);

    insert(this, this.end, &&other);

    return &this;
  }

  pub fn with_size(usize size, Allocator &allocator = Allocator()) -> list
  {
    var result = list(allocator);

    result.resize(size);

    return result;
  }

  pub fn from(Range<T> &&other, Allocator &allocator = Allocator()) -> list
  {
    var result = list(allocator);

    result.insert(result.end, &&other);

    return result;
  }

  pub fn from<U>(Range<U> &&other, Allocator &allocator = Allocator()) -> list
    match (U u) { T(u, allocator?: std::declval<Allocator>()); }
    where !__is_same<T, U>
  {
    var result = list(allocator);

    result.insert(result.end, &&other);

    return result;
  }

  pub ~list()
  {
    clear(this);
  }

  pub fn len(this &) -> usize
  {
    return this._len;
  }

  pub fn empty(this &) -> bool
  {
    return this._len == 0;
  }

  pub fn allocator(this &) -> Allocator &
  {
    return &this._allocator;
  }

  pub struct iterator
  {
    pub iterator() = default;
    pub iterator(iterator&) = default;
    pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
    pub ~iterator() = default;

    pub fn *(iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    pub fn *(iterator mut &this) -> T mut &
    {
      return &cast<Node mut *>(this.cursor).value;
    }

    pub fn ++(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    pub fn --(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    iterator(Link mut *node)
      : cursor(node)
    {
    }

    Link mut *cursor;
  }

  pub struct move_iterator
  {
    pub move_iterator() = default;
    pub move_iterator(move_iterator&) = default;
    pub fn =(move_iterator mut &this, move_iterator &that) -> move_iterator mut & = default;
    pub ~move_iterator() = default;

    pub fn *(move_iterator &this) -> T &
    {
      return &cast<Node*>(this.cursor).value;
    }

    pub fn *(move_iterator mut &this) -> T &&
    {
      return &std::move(cast<Node mut *>(this.cursor).value);
    }

    pub fn ++(move_iterator mut &this) -> move_iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    pub fn --(move_iterator mut &this) -> move_iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    move_iterator(Link mut *node)
      : cursor(node)
    {
    }

    Link mut *cursor;
  }

  pub struct const_iterator
  {
    pub const_iterator() = default;
    pub const_iterator(const_iterator&) = default;
    pub fn =(const_iterator mut &this, const_iterator &that) -> const_iterator mut & = default;
    pub ~const_iterator() = default;

    pub const_iterator(iterator other)
      : cursor(other.cursor)
    {
    }

    pub fn *(const_iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    pub fn ++(const_iterator mut &this) -> const_iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    pub fn --(const_iterator mut &this) -> const_iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    const_iterator(Link *node)
      : cursor(node)
    {
    }

    Link *cursor;
  }
  
  pub concept Iterator
  {
    requires<T> __is_same<T, iterator> || __is_same<T, move_iterator> || __is_same<T, const_iterator>;
  }

  pub fn ==(Iterator &lhs, Iterator &rhs) -> bool
  {
    return lhs.cursor == rhs.cursor;
  }

  pub fn begin(this &&)
  {
    return iter(&&this, this._root.next);
  }

  pub fn end(this &&)
  {
    return iter(&&this, &this._root);
  }

  pub fn front(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.next).value;
  }

  pub fn back(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.prev).value;
  }

  pub fn push_front<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(args..., allocator?: std::declval<Allocator>()); }
  {
    return &insert(this, this.begin.cursor, &&args...).value;
  }

  pub fn push_back<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(args..., allocator?: std::declval<Allocator>()); }
  {
    return &insert(this, this.end.cursor, &&args...).value;
  }

  pub fn insert<Args>(this mut &, Iterator position, Args && ...args) -> iterator
    match (Args args) { T(args..., allocator?: std::declval<Allocator>()); }
  {
    return iterator(insert(this, position.cursor, &&args...));
  }

  pub fn insert<U>(this mut &, Iterator position, Range<U> &&other) -> iterator
    match (U u) { T(u, allocator?: std::declval<Allocator>()); }
  {
    #if (__is_rvalue<typeof(other)> && __is_same<list, typeof(other)>)
    {
      return this.splice(position, other);
    }

    return this.insert(position, (&&other).begin, (&&other).end);
  }

  pub fn insert<I>(this mut &, Iterator position, I first, Sentinal<I> last) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      node = insert(this, position.cursor, *first++);

      while (first != last)
      {
        insert(this, position.cursor, *first++);
      }
    }

    return iterator(node);
  }

  pub fn append(this mut &, Range<T> &&other) -> iterator
  {
    return insert(this, this.end, &&other);
  }

  pub fn append<I>(this mut &, I first, Sentinal<I> last) -> iterator
  {
    return insert(this, this.end, first, last);
  }

  pub fn splice(this mut &, Iterator position, list mut &other) -> iterator
  {
    assert(&this != &other);
    assert(this.allocator == other.allocator);

    var x = cast<Link mut *>(position.cursor);

    if (other.len != 0)
    {
      x = splice(this, position.cursor, other, other.begin.cursor, other.end.cursor, other.len);
    }

    return iterator(x);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first) -> iterator
  {
    assert(this.allocator == other.allocator);

    var x = cast<Link mut *>(position.cursor);

    if (first != other.end)
    {
      x = splice(this, position.cursor, other, first.cursor, first.cursor.next, 1);
    }

    return iterator(x);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first, Iterator last) -> iterator
  {
    assert(this.allocator == other.allocator);

    var x = cast<Link mut *>(position.cursor);

    if (first != last)
    {
      x = splice(this, position.cursor, other, first.cursor, last.cursor, std::distance(first, last));
    }

    return iterator(x);
  }

  pub fn pop_front(this mut &) -> void
  {
    assert(!this.empty);
    
    erase(this, this._root.next, this._root.next.next);
  }

  pub fn pop_back(this mut &) -> void
  {
    assert(!this.empty);

    erase(this, this._root.prev, this._root.prev.next);
  }
  
  pub fn erase(this mut &, Iterator position) -> iterator
  {
    if (position == this.end)
      return iterator(position);

    return iterator(erase(this, position.cursor, position.cursor.next));
  }

  pub fn erase(this mut &, Iterator first, Iterator last) -> iterator
  {
    return iterator(erase(this, first.cursor, last.cursor));
  }

  pub fn resize(this mut &, usize size) -> void
  {
    while (size < this._len)
      this.pop_back();

    while (this._len < size)
      this.push_back();
  }

  pub fn clear(this mut &) -> void
  {
    erase(this, this._root.next, &this._root);
  }
  
  struct Link
  {
    Link mut *next;
    Link mut *prev;

    Link() = default;
    ~Link() = default;
  }

  struct Node : Link
  {
    Node()
      : value(void)
    {
    }

    ~Node() = default;

    T value;
  }

  fn init(this mut &)
  {
    this._len = 0;
    this._root.next = this._root.prev = &this._root;
  }

  fn iter(this &&, var node)
  {
    #if (__is_const<typeof(this)>)
      return const_iterator(node);

    #else if (__is_rvalue<typeof(this)>)
      return move_iterator(node);

    #else
      return iterator(node);
  }

  fn insert<Args>(this mut &, Link *position, Args && ...args) -> Node mut *
  {
    var node = new<Node>(this._allocator.allocate(sizeof<Node>, alignof<Node>));

    new<T>(&node.value)(&&args..., allocator?: this.allocator);

    node.next = cast(position);
    node.prev = cast(position.prev);
    node.next.prev = node.prev.next = node;

    this._len += 1;
    
    return node;
  }

  fn splice(this mut &, Link *position, list mut &other, Link *first, Link *last, usize count) -> Link mut *
  {
    var node = cast<Link mut *>(first);
    var last = cast<Link mut *>(last.prev);

    last.next.prev = node.prev;
    node.prev.next = last.next;

    last.next = cast(position);
    node.prev = cast(position.prev);
    last.next.prev = last;
    node.prev.next = node;

    this._len += count;
    other._len -= count;

    return node;
  }

  fn erase(this mut &, Link *first, Link *last) -> Link mut *
  {
    var count = 0;
    var node = cast<Link mut *>(first);
    var prev = cast<Link mut *>(first.prev);
    var last = cast<Link mut *>(last);

    while (node != last)
    {
      var next = node.next;

      Node::~Node(*cast<Node mut *>(node));
      this._allocator.free(node, sizeof<Node>);

      count += 1;
      node = next;
    }

    prev.next = last;
    last.prev = prev;

    this._len -= count;

    return node;
  }

  Link _root;
  usize _len;

  Allocator _allocator;
}

pub fn hash<T>(var &hasher, list<T> &lst) -> usize
{
  return hasher.combine_range(lst.begin, lst.end);
}

pub fn erase<T, Value>(list<T> mut &lst, Value &value) -> usize
  match (Value value) { T(value); }
{
  var n = lst.len;
  var first = lst.begin;

  for (; first != lst.end; )
  {
    if (*first == value)
      first = lst.erase(first);
    else
      ++first;
  }

  return n - lst.len;
}

pub fn erase_if<T, Pred>(list<T> mut &lst, Pred &pred) -> usize
  where requires (T i, Pred &pred) { pred(i); }
{
  var n = lst.len;
  var first = lst.begin;

  for (; first != lst.end; )
  {
    if (pred(*first))
      first = lst.erase(first);
    else
      ++first;
  }

  return n - lst.len;
}
