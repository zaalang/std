//
// std list
//

import std.alloc;
import std.stdlib : assert;
import std.algorithm : swap;
import std.range : Sentinal, Range;
import std.type : as_const;

pub struct list<T, Alloc = std::allocator>
{
  list()
  {
    init(this);
  }

  list(Alloc &allocator)
    : allocator(allocator)
  {
    init(this);
  }

  list(list<T> &&other, Alloc &allocator = Alloc())
    : allocator(allocator)
  {
    init(this);

    #if (__is_rvalue<typeof(other)>)
    {
      swap(this, other);
    }
    #else
    {
      this.insert(this.end, other.begin, other.end);
    }
  }

  fn =(this mut &, list &&other) -> list mut &
  {
    if (&this == &other)
      return &this;

    #if (__is_rvalue<typeof(other)>)
    {
      swap(this, other);
    }
    #else
    {
      clear(this);

      this.insert(this.end, other.begin, other.end);
    }

    return &this;
  }

  fn from(Range<T> &&other, Alloc &allocator = Alloc()) -> list
  {
    var result = list<T>(allocator);

    result.insert(result.end, &&other);

    return result;
  }

  ~list()
  {
    clear(this);
  }

  pub fn len(this &) -> usize
  {
    return this._len;
  }

  pub fn empty(this &) -> bool
  {
    return this._len == 0;
  }

  pub fn begin(this mut &) -> iterator
  {
    return iterator(this._root.next);
  }

  pub fn end(this mut &) -> iterator
  {
    return iterator(&this._root);
  }

  pub fn begin(this &) -> const_iterator
  {
    return const_iterator(this._root.next);
  }

  pub fn end(this &) -> const_iterator
  {
    return const_iterator(&this._root);
  }

  pub struct iterator
  {
    iterator() = default;
    iterator(iterator&) = default;
    fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
    ~iterator() = default;

    iterator(Link mut *node)
      : cursor(node)
    {
    }

    fn ==(iterator &lhs, iterator &rhs) -> bool
    {
      return lhs.cursor == rhs.cursor;
    }

    fn *(iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    fn *(iterator mut &this) -> T mut &
    {
      return &cast<Node mut *>(this.cursor).value;
    }

    fn ++(iterator mut &this) -> iterator &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    fn --(iterator mut &this) -> iterator &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    Link mut *cursor;
  }

  pub struct const_iterator
  {
    const_iterator() = default;
    const_iterator(const_iterator&) = default;
    fn =(const_iterator mut &this, const_iterator &that) -> const_iterator mut & = default;
    ~const_iterator() = default;

    const_iterator(Link *node)
      : cursor(node)
    {
    }

    const_iterator(iterator other)
      : cursor(other.cursor)
    {
    }

    fn ==(const_iterator &lhs, const_iterator &rhs) -> bool
    {
      return lhs.cursor == rhs.cursor;
    }

    fn *(const_iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    fn ++(const_iterator mut &this) -> const_iterator &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    fn --(const_iterator mut &this) -> const_iterator &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    Link *cursor;
  }
  
  pub concept Iterator
  {
    requires<T> __is_same<T, iterator> || __is_same<T, const_iterator>;
  }

  fn ==(Iterator &lhs, Iterator &rhs) -> bool
  {
    return lhs.cursor == rhs.cursor;
  }

  pub fn front(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.next).value;
  }

  pub fn back(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.prev).value;
  }

  pub fn push_front(this mut &, T &&value) -> T mut &
  {
    return &add_node(this, this.begin.cursor, &&value).value;
  }

  pub fn push_back(this mut &, T &&value) -> T mut &
  {
    return &add_node(this, this.end.cursor, &&value).value;
  }

  pub fn insert(this mut &, Iterator position, T &&value) -> iterator
  {
    return iterator(add_node(this, position.cursor, &&value));
  }

  pub fn insert(this mut &, Iterator position, Range<T> &&other) -> iterator
  {
    #if (__is_same<list, typeof(other)> && __is_rvalue<typeof(other)>)
    {
      return this.splice(position, other);
    }
    //#else if (__is_rvalue<typeof(other)>) // TODO
    //{
    //  return this.insert(position, std::move_iterator(other.begin), std::move_iterator(other.end));
    //}
    #else
    {
      return this.insert(position, other.begin, other.end);
    }
  }

  pub fn insert<I>(this mut &, Iterator position, I first, Sentinal<I> last) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      node = add_node(this, position.cursor, *first++);

      while (first != last)
        add_node(this, position.cursor, *first++);
    }

    return iterator(node);
  }

  pub fn splice(this mut &, Iterator position, list mut &other) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (other._len != 0)
    {
      node = move_node(this, position.cursor, cast<Node mut *>(other._root.next));

      while (other._root.next != &other._root)
        move_node(this, position.cursor, cast<Node mut *>(other._root.next));

      this._len += other.len;
      other._len = 0;
    }

    return iterator(node);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != other.end)
    {
      node = move_node(this, position.cursor, cast<Node mut *>(first.cursor));

      this._len += 1;
      other._len -= 1;
    }

    return iterator(node);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first, Iterator last) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      var count = 1;
      var current = first.cursor;

      var tmp = current;
      current = current.next;

      node = move_node(this, position.cursor, cast<Node mut *>(tmp));

      while (current != last.cursor)
      {
        tmp = current;
        current = current.next;

        move_node(this, position.cursor, cast<Node mut *>(tmp));

        count += 1;
      }

      this._len += count;
      other._len -= count;
    }

    return iterator(node);
  }

  pub fn emplace<Args>(this mut &, Iterator position, Args && ...args) -> iterator
  {
    return iterator(add_node(this, position.cursor, &&args...));
  }
  
  pub fn pop_front(this mut &) -> void
  {
    assert(!this.empty);
    
    erase_node(this, this._root.next);
  }

  pub fn pop_back(this mut &) -> void
  {
    assert(!this.empty);

    erase_node(this, this._root.prev);
  }
  
  pub fn erase(this mut &, Iterator position) -> iterator
  {
    return iterator(erase_node(this, position.cursor));
  }

  pub fn erase(this mut &, Iterator first, Iterator last) -> iterator
  {
    var node = cast<Node mut *>(first.cursor);
    
    while(node != last.cursor)
      node = erase_node(this, node);
  
    return iterator(node);
  }
  
  pub fn clear(this mut &) -> void
  {
    while (var current = this._root.next; current != &this._root)
    {
      var node = cast<Node mut *>(current);

      current = current.next;

      T::~T(node.value);
      this.allocator.free(node, sizeof<Node>);
    }
      
    init(this);
  }
  
  struct Link
  {
    Link mut *next;
    Link mut *prev;

    Link() = default;
    ~Link() = default;
  }

  struct Node : Link
  {
    Node()
      : value(void)
    {
    }

    ~Node() = default;

    T value;
  }

  fn init(this mut &)
  {
    this._root.next = this._root.prev = &this._root;
  }

  fn swap(this mut &, list mut &other)
  {
    this._root.next.prev = this._root.prev.next = &other._root;
    other._root.next.prev = other._root.prev.next = &this._root;

    swap(this._root.prev, other._root.prev);
    swap(this._root.next, other._root.next);

    swap(this._len, other._len);

    swap(this.allocator, other.allocator);
  }

  fn add_node<Args>(this mut &, Link *position, Args && ...args) -> Node mut *
  {
    var node = new<Node>(this.allocator.allocate(sizeof<Node>, alignof<Node>));

    new<T>(&node.value)(&&args...);

    node.next = cast(position);
    node.prev = cast(position.prev);
    node.next.prev = node.prev.next = node;

    this._len += 1;
    
    return node;
  }

  fn move_node(this mut &, Link *position, Node mut *node) -> Node mut *
  {
    node.next.prev = node.prev;
    node.prev.next = node.next;

    node.next = cast(position);
    node.prev = cast(position.prev);
    node.next.prev = node.prev.next = node;

    return node;
  }

  fn erase_node(this mut &, Link *position) -> Node mut *
  {
    assert(position != &this._root);

    var node = cast<Node mut *>(position);
    var nextnode = cast<Node mut *>(position.next);

    node.prev.next = node.next;
    node.next.prev = node.prev;

    T::~T(node.value);
    this.allocator.free(node, sizeof<Node>);
    
    this._len -= 1;
    
    return nextnode;
  }
  
  Link _root;
  usize _len;

  Alloc allocator;
}
