//
// std list
//

import std.alloc;
import std.stdlib : assert;
import std.algorithm : swap;
import std.range : Sentinal, Range;
import std.type : as_const;

pub struct list<T, Alloc = std::allocator>
{
  const is_owning_container = true;

  list(Alloc &allocator = Alloc())
    : allocator(allocator)
  {
    init(this);
  }

  list(list &&other, Alloc &allocator = Alloc())
    : allocator(allocator)
  {
    init(this);

    insert(this, this.end, &&other);
  }

  pub fn =(this mut &, list &&other) -> list mut &
  {
    if (&this == &other)
      return &this;

    clear(this);

    insert(this, this.end, &&other);

    return &this;
  }

  pub fn with_size(usize size, Alloc &allocator = Alloc()) -> list
  {
    var result = list(allocator);

    result.resize(size);

    return result;
  }

  pub fn from(Range<T> &&other, Alloc &allocator = Alloc()) -> list
  {
    var result = list(allocator);

    result.insert(result.end, &&other);

    return result;
  }

  ~list()
  {
    clear(this);
  }

  pub fn len(this &) -> usize
  {
    return this._len;
  }

  pub fn empty(this &) -> bool
  {
    return this._len == 0;
  }

  pub fn begin(this mut &) -> iterator
  {
    return iterator(this._root.next);
  }

  pub fn end(this mut &) -> iterator
  {
    return iterator(&this._root);
  }

  pub fn begin(this &) -> const_iterator
  {
    return const_iterator(this._root.next);
  }

  pub fn end(this &) -> const_iterator
  {
    return const_iterator(&this._root);
  }

  pub struct iterator
  {
    iterator() = default;
    iterator(iterator&) = default;
    fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
    ~iterator() = default;

    iterator(Link mut *node)
      : cursor(node)
    {
    }

    fn ==(iterator &lhs, iterator &rhs) -> bool
    {
      return lhs.cursor == rhs.cursor;
    }

    fn *(iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    fn *(iterator mut &this) -> T mut &
    {
      return &cast<Node mut *>(this.cursor).value;
    }

    fn ++(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    fn --(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    Link mut *cursor;
  }

  pub struct const_iterator
  {
    const_iterator() = default;
    const_iterator(const_iterator&) = default;
    fn =(const_iterator mut &this, const_iterator &that) -> const_iterator mut & = default;
    ~const_iterator() = default;

    const_iterator(Link *node)
      : cursor(node)
    {
    }

    const_iterator(iterator other)
      : cursor(other.cursor)
    {
    }

    fn ==(const_iterator &lhs, const_iterator &rhs) -> bool
    {
      return lhs.cursor == rhs.cursor;
    }

    fn *(const_iterator &this) -> T&
    {
      return &cast<Node*>(this.cursor).value;
    }

    fn ++(const_iterator mut &this) -> const_iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    fn --(const_iterator mut &this) -> const_iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    Link *cursor;
  }
  
  pub concept Iterator
  {
    requires<T> __is_same<T, iterator> || __is_same<T, const_iterator>;
  }

  fn ==(Iterator &lhs, Iterator &rhs) -> bool
  {
    return lhs.cursor == rhs.cursor;
  }

  pub fn front(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.next).value;
  }

  pub fn back(this &&)
  {
    assert(!this.empty);

    return &cast<Node&&>(*this._root.prev).value;
  }

  pub fn push_front<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(args...); }
  {
    return &add_node(this, this.begin.cursor, &&args...).value;
  }

  pub fn push_back<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(args...); }
  {
    return &add_node(this, this.end.cursor, &&args...).value;
  }

  pub fn insert<Args>(this mut &, Iterator position, Args && ...args) -> iterator
    match (Args args) { T(args...); }
  {
    return iterator(add_node(this, position.cursor, &&args...));
  }

  pub fn insert(this mut &, Iterator position, Range<T> &&other) -> iterator
  {
    #if (__is_same<list, typeof(other)> && __is_rvalue<typeof(other)>)
    {
      return this.splice(position, other);
    }
    #else if (__is_rvalue<typeof(other)> && (__is_array<typeof(other)> || requires { typeof(other)::is_owning_container; } && typeof(other)::is_owning_container))
    {
      return this.insert(position, std::move_iterator(other.begin), std::move_iterator(other.end));
    }
    #else
    {
      return this.insert(position, other.begin, other.end);
    }
  }

  pub fn insert<I>(this mut &, Iterator position, I first, Sentinal<I> last) -> iterator
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      node = add_node(this, position.cursor, *first++);

      while (first != last)
        add_node(this, position.cursor, *first++);
    }

    return iterator(node);
  }

  pub fn append(this mut &, Range<T> &&other) -> iterator
  {
    return insert(this, this.end, &&other);
  }

  pub fn append<I>(this mut &, I first, Sentinal<I> last) -> iterator
  {
    return insert(this, this.end, first, last);
  }

  pub fn splice(this mut &, Iterator position, list mut &other) -> iterator
  {
    assert(&this != &other);
    assert(this.allocator == other.allocator);

    var node = cast<Node mut *>(position.cursor);

    if (other.len != 0)
    {
      node = transfer_nodes(this, position.cursor, other, other.begin.cursor, other.end.cursor, other.len);
    }

    return iterator(node);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first) -> iterator
  {
    assert(this.allocator == other.allocator);

    var node = cast<Node mut *>(position.cursor);

    if (first != other.end)
    {
      node = transfer_nodes(this, position.cursor, other, first.cursor, first.cursor.next, 1);
    }

    return iterator(node);
  }

  pub fn splice(this mut &, Iterator position, list mut &other, Iterator first, Iterator last) -> iterator
  {
    assert(this.allocator == other.allocator);

    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      node = transfer_nodes(this, position.cursor, other, first.cursor, last.cursor, std::distance(first, last));
    }

    return iterator(node);
  }

  pub fn pop_front(this mut &) -> void
  {
    assert(!this.empty);
    
    erase_node(this, this._root.next);
  }

  pub fn pop_back(this mut &) -> void
  {
    assert(!this.empty);

    erase_node(this, this._root.prev);
  }
  
  pub fn erase(this mut &, Iterator position) -> iterator
  {
    return iterator(erase_node(this, position.cursor));
  }

  pub fn erase(this mut &, Iterator first, Iterator last) -> iterator
  {
    var node = cast<Node mut *>(first.cursor);
    
    while(node != last.cursor)
      node = erase_node(this, node);
  
    return iterator(node);
  }

  pub fn resize(this mut &, usize size) -> void
  {
    while (size < this._len)
      this.pop_back();

    while (this._len < size)
      this.push_back();
  }

  pub fn clear(this mut &) -> void
  {
    while (var current = this._root.next; current != &this._root)
    {
      var node = cast<Node mut *>(current);

      current = current.next;

      T::~T(node.value);
      this.allocator.free(node, sizeof<Node>);
    }
      
    init(this);
  }
  
  struct Link
  {
    Link mut *next;
    Link mut *prev;

    Link() = default;
    ~Link() = default;
  }

  struct Node : Link
  {
    Node()
      : value(void)
    {
    }

    ~Node() = default;

    T value;
  }

  fn init(this mut &)
  {
    this._len = 0;
    this._root.next = this._root.prev = &this._root;
  }

  fn add_node<Args>(this mut &, Link *position, Args && ...args) -> Node mut *
  {
    var node = new<Node>(this.allocator.allocate(sizeof<Node>, alignof<Node>));

    #if (__is_allocator_aware<T>)
      new<T>(&node.value)(&&args..., allocator: this.allocator);
    #else
      new<T>(&node.value)(&&args...);

    node.next = cast(position);
    node.prev = cast(position.prev);
    node.next.prev = node.prev.next = node;

    this._len += 1;
    
    return node;
  }

  fn transfer_nodes(this mut &, Link *position, list mut &other, Link *first, Link *last, usize count) -> Node mut *
  {
    var node = cast<Node mut *>(first);
    var last = cast<Link mut *>(last.prev);

    last.next.prev = node.prev;
    node.prev.next = last.next;

    last.next = cast(position);
    node.prev = cast(position.prev);
    last.next.prev = last;
    node.prev.next = node;

    this._len += count;
    other._len -= count;

    return node;
  }

  fn erase_node(this mut &, Link *position) -> Node mut *
  {
    assert(position != &this._root);

    var node = cast<Node mut *>(position);
    var nextnode = cast<Node mut *>(position.next);

    node.prev.next = node.next;
    node.next.prev = node.prev;

    T::~T(node.value);
    this.allocator.free(node, sizeof<Node>);
    
    this._len -= 1;
    
    return nextnode;
  }

  Link _root;
  usize _len;

  allocator: Alloc;
}
