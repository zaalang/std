//
// std list
//

import std.memory;
import std.stdlib : assert;
import std.algorithm : find, find_if, find_if_not, count, count_if, count_if_not, sort!, uniq, uniq!, is_sorted;
import std.algorithm.iteration : each, min, max, sum, mean;
import std.range : Sentinal, Range;
import std.type : as_const;

pub struct list<T, Allocator = std::allocator>
{
  pub list(Allocator &allocator = Allocator())
    : _allocator(allocator)
  {
    init(&mut this);
  }

  pub list(list &&other, Allocator &allocator = Allocator())
    : _allocator(allocator)
  {
    init(&mut this);

    insert(&mut this, this.end, &&other);
  }

  #[lifetime(poison(this), launder(this), repose(other, this), assign(this, depend(*other)), depend(this))]
  pub fn =(this mut &, list &&other) -> list mut &
  {
    std::launder(other);

    if (&this != &other)
    {
      clear(&mut this);

      insert(&mut this, this.end, &&other);
    }

    return &this;
  }

  pub fn with_size(usize size, Allocator &allocator = Allocator()) -> list
  {
    var result = list(allocator);

    resize(&mut result, size);

    return result;
  }

  pub fn from(Range<T> &&other, Allocator &allocator = Allocator()) -> list
  {
    var result = list(allocator);

    insert(&mut result, result.end, &&other);

    return result;
  }

  pub fn from<U>(Range<U> &&other, Allocator &allocator = Allocator()) -> list
    match (U u) { T(&move u, allocator?: std::declval<Allocator>()); }
    where !__is_same<U, T>
  {
    var result = list(allocator);

    insert(&mut result, result.end, &&other);

    return result;
  }

  pub ~list()
  {
    clear(&mut this);
  }

  pub fn len(this &) -> usize
  {
    return this._len;
  }

  pub fn empty(this &) -> bool
  {
    return this._len == 0;
  }

  pub fn allocator(this &) -> Allocator &
  {
    return &this._allocator;
  }

  pub struct iterator
  {
    pub iterator() = default;
    pub iterator(iterator&) = default;
    pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
    pub ~iterator() = default;

    #[lifetime(depend(*this))]
    pub fn ++(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.next;
      return &this;
    }

    #[lifetime(depend(*this))]
    pub fn --(iterator mut &this) -> iterator mut &
    {
      this.cursor = this.cursor.prev;
      return &this;
    }

    iterator(Link *node)
      : cursor(node)
    {
    }

    Link *cursor;
  }

  pub fn ==(iterator lhs, iterator rhs) -> bool
  {
    return lhs.cursor == rhs.cursor;
  }

  #[lifetime(depend(this))]
  pub fn begin(this &)
  {
    return iterator(this._root.next);
  }

  #[lifetime(depend(this))]
  pub fn end(this &)
  {
    return iterator(&this._root);
  }

  #[lifetime(repose(iter, this), depend(this))]
  pub fn [](this &&, iterator iter)
  {
    return &cast<&&typeof(this)>(cast<Node mut *>(iter.cursor).value);
  }

  #[lifetime(depend(this))]
  pub fn front(this &&)
  {
    assert(this._len != 0);

    return &cast<&&typeof(this)>(cast<Node mut *>(this._root.next).value);
  }

  #[lifetime(depend(this))]
  pub fn back(this &&)
  {
    assert(this._len != 0);

    return &cast<&&typeof(this)>(cast<Node mut *>(this._root.prev).value);
  }

  #[lifetime(repose(args, this), depend(this))]
  pub fn push_front<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(&move args..., allocator?: std::declval<Allocator>()); }
  {
    return &insert(&mut this, this.begin.cursor, &&args...).value;
  }

  #[lifetime(repose(args, this), depend(this))]
  pub fn push_back<Args>(this mut &, Args && ...args) -> T mut &
    match (Args args) { T(&move args..., allocator?: std::declval<Allocator>()); }
  {
    return &insert(&mut this, this.end.cursor, &&args...).value;
  }

  #[lifetime(repose(position, this), repose(args, this), depend(this))]
  pub fn insert<Args>(this mut &, iterator position, Args && ...args) -> iterator
    match (Args args) { T(&move args..., allocator?: std::declval<Allocator>()); }
  {
    return iterator(insert(&mut this, position.cursor, &&args...));
  }

  #[lifetime(repose(position, this), depend(this))]
  pub fn insert<U>(this mut &, iterator position, list &&other) -> iterator
    where __is_rvalue<typeof(other)>
  {
    return splice(&mut this, position, &mut other);
  }

  #[lifetime(repose(position, this), depend(this))]
  pub fn insert<U>(this mut &, iterator position, Range<U> &&other) -> iterator
    match (U u) { T(&move u, allocator?: std::declval<Allocator>()); }
  {
    var node = cast<Node mut *>(position.cursor);

    if (var first = other.begin, var last = other.end; first != last)
    {
      node = insert(&mut this, position.cursor, (&&other)[first++]);

      while (first != last)
      {
        insert(&mut this, position.cursor, (&&other)[first++]);
      }
    }

    return iterator(node);
  }

  #[lifetime(repose(position, this), depend(this))]
  pub fn insert<I>(this mut &, iterator position, I first, Sentinal<I> last) -> iterator
    where requires (I i) { T(*i); }
  {
    var node = cast<Node mut *>(position.cursor);

    if (first != last)
    {
      node = insert(&mut this, position.cursor, *first++);

      while (first != last)
      {
        insert(&mut this, position.cursor, *first++);
      }
    }

    return iterator(node);
  }

  #[lifetime(repose(position, this), repose(args, this), depend(this))]
  pub fn emplace<Args>(this mut &, iterator position, Args && ...args) -> T mut &
    match (Args args) { T(&move args..., allocator?: std::declval<Allocator>()); }
  {
    return &insert(&mut this, position.cursor, &&args...).value;
  }

  #[lifetime(depend(this))]
  pub fn append<U>(this mut &, Range<U> &&other) -> iterator
    match (U u) { T(&move u, allocator?: std::declval<Allocator>()); }
  {
    return insert(&mut this, this.end, &&other);
  }

  #[lifetime(depend(this))]
  pub fn append<I>(this mut &, I first, Sentinal<I> last) -> iterator
    where requires (I i) { T(*i); }
  {
    return insert(&mut this, this.end, first, last);
  }

  #[lifetime(repose(position, this), depend(this))]
  pub fn splice(this mut &, iterator position, list mut &other) -> iterator
  {
    assert(&this != &other);
    assert(this.allocator == other.allocator);

    var node = cast<Link mut *>(position.cursor);

    if (other.len != 0)
    {
      node = splice(&mut this, position.cursor, &mut other, other.begin.cursor, other.end.cursor, other.len);
    }

    return iterator(node);
  }

  #[lifetime(repose(position, this), repose(position, other), repose(first, this), repose(first, other), repose(other, this), repose(this, other), depend(this))]
  pub fn splice(this mut &, iterator position, list mut &other, iterator first) -> iterator
  {
    assert(this.allocator == other.allocator);

    var node = cast<Link mut *>(position.cursor);

    if (first != other.end)
    {
      node = splice(&mut this, position.cursor, &mut other, first.cursor, first.cursor.next, 1);
    }

    return iterator(node);
  }

  #[lifetime(repose(position, this), repose(position, other), repose(first, this), repose(first, other), repose(last, this), repose(last, other), repose(other, this), repose(this, other), depend(this))]
  pub fn splice(this mut &, iterator position, list mut &other, iterator first, iterator last) -> iterator
  {
    assert(this.allocator == other.allocator);

    var node = cast<Link mut *>(position.cursor);

    if (first != last)
    {
      node = splice(&mut this, position.cursor, &mut other, first.cursor, last.cursor, std::distance(first, last));
    }

    return iterator(node);
  }

  #[lifetime(poison(this))]
  pub fn pop_front(this mut &) -> void
  {
    assert(!this.empty);
    
    erase(&mut this, this._root.next, this._root.next.next);
  }

  #[lifetime(poison(this))]
  pub fn pop_back(this mut &) -> void
  {
    assert(!this.empty);

    erase(&mut this, this._root.prev, this._root.prev.next);
  }
  
  #[lifetime(poison(this))]
  pub fn take_front(this mut &) -> T
  {
    var result = &move this.front;

    std::launder(this);

    erase(&mut this, this._root.next, this._root.next.next);

    return result;
  }

  #[lifetime(poison(this))]
  pub fn take_back(this mut &) -> T
  {
    var result = &move this.back;

    std::launder(this);

    erase(&mut this, this._root.prev, this._root.prev.next);

    return result;
  }

  #[lifetime(poison(this), repose(position, this), depend(this))]
  pub fn erase(this mut &, iterator position) -> iterator
  {
    if (position == this.end)
      return iterator(position);

    return iterator(erase(&mut this, position.cursor, position.cursor.next));
  }

  #[lifetime(poison(this), repose(first, this), repose(last, this), depend(this))]
  pub fn erase(this mut &, iterator first, iterator last) -> iterator
  {
    return iterator(erase(&mut this, first.cursor, last.cursor));
  }

  #[lifetime(poison(this), repose(position, this))]
  pub fn take_erase(this mut &, iterator position) -> T
  {
    var result = &move this[position];

    std::launder(this);

    erase(&mut this, position);

    return result;
  }

  #[lifetime(repose(first, this), repose(last, this))]
  pub fn take(this mut &, iterator first, iterator last) -> list
  {
    var result = list();

    result.splice(result.begin, &mut this, first, last);

    return result;
  }

  pub fn resize(this mut &, usize size) -> void
  {
    while (size < this._len)
      this.pop_back();

    while (this._len < size)
      this.push_back();
  }

  #[lifetime(launder(this), poison(this))]
  pub fn clear(this mut &) -> void
  {
    erase(&mut this, this._root.next, &this._root);
  }
  
  struct Link
  {
    Link mut *next;
    Link mut *prev;

    Link() = default;
    ~Link() = default;
  }

  struct Node : pub Link
  {
    Node()
      : value(void)
    {
    }

    ~Node() = default;

    T value;
  }

  fn init(this mut &)
  {
    this._len = 0;
    this._root.next = this._root.prev = &this._root;
  }

  fn insert<Args>(this mut &, Link *position, Args && ...args) -> Node mut *
  {
    var node = new<Node>(this._allocator.allocate(sizeof<Node>, alignof<Node>));

    new<T>(&node.value)(&&args..., allocator?: this.allocator);

    node.next = cast(position);
    node.prev = cast(position.prev);
    node.next.prev = node.prev.next = node;

    this._len += 1;
    
    return node;
  }

  fn splice(this mut &, Link *position, list mut &other, Link *first, Link *last, usize count) -> Link mut *
  {
    var node = cast<Link mut *>(first);
    var last = cast<Link mut *>(last.prev);

    last.next.prev = node.prev;
    node.prev.next = last.next;

    last.next = cast(position);
    node.prev = cast(position.prev);
    last.next.prev = last;
    node.prev.next = node;

    this._len += count;
    other._len -= count;

    return node;
  }

  fn erase(this mut &, Link *first, Link *last) -> Link mut *
  {
    var count = 0;
    var node = cast<Link mut *>(first);
    var prev = cast<Link mut *>(first.prev);
    var last = cast<Link mut *>(last);

    while (node != last)
    {
      var next = node.next;

      Node::~Node(&mut *cast<Node mut *>(node));
      this._allocator.free(node, sizeof<Node>);

      count += 1;
      node = next;
    }

    prev.next = last;
    last.prev = prev;

    this._len -= count;

    return node;
  }

  Link _root;
  usize _len;

  Allocator _allocator;
}

pub fn hash<T>(var &hasher, list<T> &lst) -> usize
{
  return hasher.combine_range(lst, lst.begin, lst.end);
}

#[lifetime(poison(range), depend(range))]
pub fn drop_n!<T>(list<T> mut &range, usize n)
{
  range.erase(range.begin, std::next(range.begin, n, range.end));

  return &range;
}

#[lifetime(poison(range), depend(range))]
pub fn take_n!<T>(list<T> mut &range, usize n)
{
  range.erase(std::next(range.begin, n, range.end), range.end);

  return &range;
}

#[lifetime(poison(lst))]
pub fn erase<T, Value>(list<T> mut &lst, Value &value) -> usize
  match (T u, Value v) { u == v; }
{
  var n = lst.len;
  var first = lst.begin;

  for (; first != lst.end; )
  {
    if (lst[first] == value)
      first = lst.erase(first);
    else
      ++first;
  }

  return n - lst.len;
}

#[lifetime(poison(lst))]
pub fn erase_if<T, Pred>(list<T> mut &lst, Pred &pred) -> usize
  where requires (T i, Pred &pred) { pred(i); }
{
  var n = lst.len;
  var first = lst.begin;

  for (; first != lst.end; )
  {
    if (pred(lst[first]))
      first = lst.erase(first);
    else
      ++first;
  }

  return n - lst.len;
}
