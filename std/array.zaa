//
// std array
//

import std.span;
import std.stdlib : assert;
import std.range : Range;
import std.span : span, mutable_span;
import std.type : as_const;

pub struct array<T, N> : T[N]
{
  array() = default;
  array(array&&) = default;
  fn =(this mut &, array &&) -> array mut & = default;

  array(T[N] &&that)
    : super(&&that)
  {
  }

  fn =(this mut &, T[N] &&other) -> array mut &
  {
    this.super = &&other;
    return &this;
  }

  fn with_size(#usize size) -> array<T, size>
  {
    return array<T, size>();
  }

  ~array() = default;

  pub fn [](this &, (usize, usize) range) -> span<T>
  {
    assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

    return span(this.data + range.0, this.data + range.1);
  }

  pub fn [](this mut &, (usize, usize) range) -> mutable_span<T>
  {
    assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

    return mutable_span(this.data + range.0, this.data + range.1);
  }

  pub struct iterator
  {
    iterator() = default;
    iterator(iterator&) = default;
    fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
    ~iterator() = default;

    iterator(T mut *item)
      : cursor(item)
    {
    }

    fn *(iterator &this) -> T&
    {
      return &*this.cursor;
    }

    fn *(iterator mut &this) -> T mut &
    {
      return &*this.cursor;
    }

    fn ++(iterator mut &this) -> iterator mut &
    {
      ++this.cursor;
      return &this;
    }

    fn --(iterator mut &this) -> iterator mut &
    {
      --this.cursor;
      return &this;
    }

    T mut *cursor;
  }

  pub struct move_iterator
  {
    move_iterator() = default;
    move_iterator(move_iterator&) = default;
    fn =(move_iterator mut &this, move_iterator &that) -> move_iterator mut & = default;
    ~move_iterator() = default;

    move_iterator(T mut *item)
      : cursor(item)
    {
    }

    fn *(move_iterator &this) -> T &
    {
      return &*this.cursor;
    }

    fn *(move_iterator mut &this) -> T &&
    {
      return &std::move(*this.cursor);
    }

    fn ++(move_iterator mut &this) -> move_iterator mut &
    {
      ++this.cursor;
      return &this;
    }

    fn --(move_iterator mut &this) -> move_iterator mut &
    {
      --this.cursor;
      return &this;
    }

    T mut *cursor;
  }

  pub struct const_iterator
  {
    const_iterator() = default;
    const_iterator(const_iterator&) = default;
    fn =(const_iterator mut &this, const_iterator &that) -> const_iterator mut & = default;
    ~const_iterator() = default;

    const_iterator(T *item)
      : cursor(item)
    {
    }

    const_iterator(iterator other)
      : cursor(other.cursor)
    {
    }

    fn *(const_iterator &this) -> T&
    {
      return &*this.cursor;
    }

    fn ++(const_iterator mut &this) -> const_iterator mut &
    {
      ++this.cursor;
      return &this;
    }

    fn --(const_iterator mut &this) -> const_iterator mut &
    {
      --this.cursor;
      return &this;
    }

    T *cursor;
  }

  pub concept Iterator
  {
    requires<T> __is_same<T, iterator> || __is_same<T, move_iterator> || __is_same<T, const_iterator>;
  }

  pub fn ==(Iterator &lhs, Iterator &rhs) -> bool
  {
    return lhs.cursor == rhs.cursor;
  }

  pub fn -(Iterator lhs, Iterator rhs) -> usize
  {
    return lhs.cursor - rhs.cursor;
  }

  pub fn +(Iterator lhs, usize rhs) -> typeof(lhs)
  {
    using Iterator = typeof(lhs);

    return Iterator(lhs.cursor + rhs);
  }

  fn iter(this &&, var item)
  {
    #if (__is_const<typeof(this)>)
      return const_iterator(item);

    #else if (__is_rvalue<typeof(this)>)
      return move_iterator(item);

    #else
      return iterator(item);
  }

  pub fn begin(this &&)
  {
    return iter(&&this, this.data);
  }

  pub fn end(this &&)
  {
    return iter(&&this, this.data + this.len);
  }

  pub fn front(this &&)
  {
    assert(!this.empty);

    return &*this._data;
  }

  pub fn back(this &&)
  {
    assert(!this.empty);

    return &*(this._data + this._len);
  }
}
