//
// std io
//

import std.stdlib : assert;
import std.memory : memcpy, memmove;
import std.string : String;
import std.span : Span;
import std.math;
import std.vector;
import std.error;

pub concept InputStream
{
  requires<T> (T is, u8 mut *buf, usize len) {
    try
    {
      len += is.read(buf, len);
    }
    catch(std::error e)
    {
    }
  }
}

pub concept OutputStream
{
  requires<T> (T os, u8 *buf, usize len) {
    try
    {
      len += os.write(buf, len);
    }
    catch(std::error e)
    {
    }
  }

  pub static fn flush<T>(T mut &os) throws(std::error) -> void
  {
    #if (requires (T os) { try { os.flush(); } catch (std::error e) { } })
      os.flush();
  }
}

pub fn OutputStream<N>(u8[N] mut &array)
{
  return std::mutable_span(array.data, N);
}

pub concept InputReader
{
  requires<T> __is_match<InputStream, T>
  requires<T> (T is) { is.buffer; }
  requires<T> (T is) { try { is.fetch(); } catch(std::error e) { } }
  requires<T> (T is) { try { is.pack_and_fetch(); } catch(std::error e) { } }
  requires<T> (T is, usize len) { is.discard(len); }
}

pub struct input_stream
{
  void mut &_stream;
  fn (*_read)(void mut &, u8 mut *, usize) throws(std::error) -> usize;

  pub fn read(this mut &, u8 mut *data, usize len) throws(std::error) -> usize
  {
    return this._read(&mut this._stream, data, len);
  }

  #[lifetime(depends(is))]
  pub input_stream<T>(T mut &is)
    : _stream(&is)
  {
    assert(__is_match<InputStream, T>);

    _read = fn (void mut &stream, u8 mut *data, usize len) throws(std::error) -> usize {
      return cast<T mut &>(stream).read(data, len);
    };
  }

  pub input_stream(input_stream &&) = default;
  pub fn =(input_stream mut &, input_stream &&) -> input_stream mut & = default;
  pub ~input_stream() = default;
}

pub struct output_stream
{
  void mut &_stream;
  fn (*_write)(void mut &, u8 *, usize) throws(std::error) -> usize;
  fn (*_flush)(void mut &) throws(std::error) -> void;

  pub fn write(this mut &, u8 *data, usize len) throws(std::error) -> usize
  {
    return this._write(&mut this._stream, data, len);
  }

  pub fn flush(this mut &) throws(std::error) -> void
  {
    this._flush(&mut this._stream);
  }

  #[lifetime(depends(os))]
  pub output_stream<T>(T mut &os)
    : _stream(&os)
  {
    assert(__is_match<OutputStream, T>);

    _write = fn (void mut &stream, u8 *data, usize len) throws(std::error) -> usize {
      return cast<T mut &>(stream).write(data, len);
    };

    _flush = fn (void mut &stream) throws(std::error) -> void {
      OutputStream::flush(&mut cast<T mut &>(stream));
    };
  }

  pub output_stream(output_stream &&) = default;
  pub fn =(output_stream mut &, output_stream &&) -> output_stream mut & = default;
  pub ~output_stream() = default;
}

pub struct buffered_reader<Allocator = std::allocator, N = 0>
{
  void mut &_stream;
  fn (*_read)(void mut &, u8 mut *, usize) throws(std::error) -> usize;

  #[lifetime(depends(is))]
  pub buffered_reader<T>(T mut &is, usize capacity, Allocator &allocator = Allocator())
    : _stream(&is), _buffer(allocator)
  {
    assert(__is_match<InputStream, T>);

    _read = fn (void mut &stream, u8 mut *data, usize len) throws(std::error) -> usize {
      return cast<T mut &>(stream).read(data, len);
    };

    _head = 0;
    _tail = 0;
    _buffer.reserve(capacity);
    _buffer.append_from_capacity(_buffer.capacity);
  }

  #[lifetime(depends(is))]
  pub buffered_reader<T>(T mut &is, Allocator &allocator = Allocator())
    : this(&mut is, N == 0 ? 8192 : N, allocator)
  {
  }

  #[lifetime(depends(is))]
  pub fn with_capacity<T>(T mut &is, usize capacity, Allocator &allocator = Allocator()) -> buffered_reader
  {
    return buffered_reader(&mut is, capacity, allocator);
  }

  pub fn capacity(this &) -> usize
  {
    return this._buffer.capacity;
  }

  #[lifetime(depends(this))]
  pub fn buffer(this &) -> std::span<u8>
  {
    return std::span<u8>(this._buffer.data + this._head, this._buffer.data + this._tail);
  }

  #[lifetime(depends(this))]
  pub fn fetch(this mut &) throws(std::error) -> std::span<u8>
  {
    if (this._head == this._tail)
    {
      this._head = 0;
      this._tail = this._read(&mut this._stream, this._buffer.data, this._buffer.len);
    }

    return this.buffer;
  }

  #[lifetime(depends(this))]
  pub fn pack_and_fetch(this mut &) throws(std::error) -> std::span<u8>
  {
    if (this._head != 0)
    {
      memmove(this._buffer.data, this._buffer.data + this._head, this._tail - this._head);
      this._tail -= this._head;
      this._head = 0;
    }

    this._tail += this._read(&mut this._stream, this._buffer.data + this._tail, this._buffer.len - this._tail);

    return this.buffer;
  }

  pub fn discard(this mut &, usize len) -> void
  {
    assert(len <= this._tail - this._head);

    this._head += len;
  }

  pub fn read(this mut &, u8 mut *data, usize len) throws(std::error) -> usize
  {
    var ch = data;

    if (this._head != this._tail)
    {
      var size = std::min(this._tail - this._head, len);

      memcpy(ch, this._buffer.data + this._head, size);

      ch += size;
      this._head += size;
    }

    if (var remaining = len - (ch - data); remaining != 0)
    {
      if (remaining < this._buffer.len)
      {
        fetch(&mut this);

        var size = std::min(this._tail - this._head, remaining);

        memcpy(ch, this._buffer.data + this._head, size);

        ch += size;
        this._head += size;
      }
      else
      {
        ch += this._read(&mut this._stream, ch, remaining);
      }
    }

    return ch - data;
  }

  pub struct line_iterator
  {
    pub line_iterator(line_iterator&) = default;
    pub ~line_iterator() = default;

    pub fn ++(line_iterator mut &this) throws(std::error) -> line_iterator mut &
    {
      this.reader.discard(this.n);

      if (var buffer = this.reader.fetch(); buffer.len != 0)
      {
        this.n = std::memchr(buffer.data, cast('\n'), buffer.len);

        if (this.n == buffer.len)
        {
          buffer = this.reader.pack_and_fetch();

          while (this.n < buffer.len)
          {
            this.n += std::memchr(buffer.data + this.n, cast('\n'), buffer.len - this.n);

            if (this.n != buffer.len)
              break;

            buffer = this.reader.pack_and_fetch();
          }

          if (this.n == this.reader.capacity)
            throw std::system_error(std::errc::line_too_long);
        }

        this.m = this.n;

        if (this.n != buffer.len)
        {
          if (this.m != 0 && buffer[this.m - 1] == cast('\r'))
            this.m -= 1;

          this.n += 1;
        }
      }
      else
      {
        this.n = this.m = 0;
      }

      return &this;
    }

    pub fn ==(line_iterator &lhs, end_iterator rhs) -> bool
    {
      return lhs.n == 0;
    }

    line_iterator(buffered_reader mut &reader)
      : reader(&reader)
    {
    }

    usize n, usize m;
    buffered_reader mut &reader;
  }

  struct end_iterator
  {
    end_iterator() = default;
    end_iterator(end_iterator&) = default;
    fn =(end_iterator mut &this, end_iterator &that) -> end_iterator mut & = default;
    ~end_iterator() = default;
  }

  #[lifetime(repose(iter, this), depend(this))]
  pub fn [](this &, line_iterator &iter)
  {
    return std::string_view(this.buffer.data, iter.m);
  }

  #[lifetime(depends(this))]
  pub fn lines(this mut &) throws(std::error)
  {
    return std::range_view(&this, ++line_iterator(&mut this), end_iterator());
  }

  usize _head;
  usize _tail;
  std::vector<u8, Allocator, N> _buffer;

  pub ~buffered_reader() = default;
}

pub fn skip_line(InputReader mut &reader) throws(std::error) -> usize
{
  var count = 0;

  for (var buffer = reader.fetch(); buffer.len != 0; buffer = reader.fetch())
  {
    var n = std::memchr(buffer.data, cast('\n'), buffer.len);

    if (n != buffer.len)
    {
      reader.discard(n + 1);
      count += n + 1;

      break;
    }

    reader.discard(n);
    count += n;
  }

  return count;
}

pub fn read_line(InputReader mut &reader, std::string mut &buf) throws(std::error) -> usize
{
  var count = 0;

  for (var buffer = reader.fetch(); buffer.len != 0; buffer = reader.fetch())
  {
    var n = std::memchr(buffer.data, cast('\n'), buffer.len);

    if (n != buffer.len)
    {
      buf.write(buffer.data, n + 1);
      reader.discard(n + 1);
      count += n + 1;

      break;
    }

    buf.write(buffer.data, n);
    reader.discard(n);
    count += n;
  }

  return count;
}

pub struct buffered_writer<Allocator = std::allocator, N = 0>
{
  void mut &_stream;
  fn (*_write)(void mut &, u8 *, usize) throws(std::error) -> usize;
  fn (*_flush)(void mut &) throws(std::error) -> void;

  #[lifetime(depends(os))]
  pub buffered_writer<T>(T mut &os, usize capacity, Allocator &allocator = Allocator())
    : _stream(&os), _buffer(allocator)
  {
    assert(__is_match<OutputStream, T>);

    _write = fn (void mut &stream, u8 *data, usize len) throws(std::error) -> usize {
      return cast<T mut &>(stream).write(data, len);
    };

    _flush = fn (void mut &stream) throws(std::error) -> void {
      OutputStream::flush(&mut cast<T mut &>(stream));
    };

    _buffer.reserve(capacity);
  }

  #[lifetime(depends(os))]
  pub buffered_writer<T>(T mut &os, Allocator &allocator = Allocator())
    : this(&mut os, N == 0 ? 8192 : N, allocator)
  {
  }

  #[lifetime(depends(os))]
  pub fn with_capacity<T>(T mut &os, usize capacity, Allocator &allocator = Allocator()) -> buffered_writer
  {
    return buffered_writer(&mut os, capacity, allocator);
  }

  pub fn capacity(this &) -> usize
  {
    return this._buffer.capacity;
  }

  pub fn write(this mut &, u8 *data, usize len) throws(std::error) -> usize
  {
    if (this._buffer.capacity - this._buffer.len < len)
    {
      flush(&mut this);
    }

    if (len < this._buffer.capacity)
    {
      this._buffer.append(data, data + len);
    }
    else
    {
      this._write(&mut this._stream, data, len);
    }

    return len;
  }

  pub fn flush(this mut &) throws(std::error) -> void
  {
    if (this._buffer.len != 0)
    {
      this._write(&mut this._stream, this._buffer.data, this._buffer.len);

      this._buffer.clear();
    }

    this._flush(&mut this._stream);
  }

  std::vector<u8, Allocator, N> _buffer;

  pub ~buffered_writer() = default;
}

pub struct line_writer<Allocator = std::allocator, N = 0>
{
  void mut &_stream;
  fn (*_write)(void mut &, u8 *, usize) throws(std::error) -> usize;
  fn (*_flush)(void mut &) throws(std::error) -> void;

  #[lifetime(depends(os))]
  pub line_writer<T>(T mut &os, usize capacity, Allocator &allocator = Allocator())
    : _stream(&os), _buffer(allocator)
  {
    assert(__is_match<OutputStream, T>);

    _write = fn (void mut &stream, u8 *data, usize len) throws(std::error) -> usize {
      return cast<T mut &>(stream).write(data, len);
    };

    _flush = fn (void mut &stream) throws(std::error) -> void {
      OutputStream::flush(&mut cast<T mut &>(stream));
    };

    _buffer.reserve(capacity);
  }

  #[lifetime(depends(os))]
  pub line_writer<T>(T mut &os, Allocator &allocator = Allocator())
    : this(&mut os, N == 0 ? 8192 : N, allocator)
  {
  }

  #[lifetime(depends(os))]
  pub fn with_capacity<T>(T mut &os, usize capacity, Allocator &allocator = Allocator()) -> line_writer
  {
    return line_writer(&mut os, capacity, allocator);
  }

  pub fn capacity(this &) -> usize
  {
    return this._buffer.capacity;
  }

  pub fn write(this mut &, u8 *data, usize len) throws(std::error) -> usize
  {
    if (len == 0)
      return 0;

    if (*(data + len - 1) == cast('\n') || this._buffer.capacity - this._buffer.len < len)
      return write_cold(&mut this, data, len);

    this._buffer.append(data, data + len);

    return len;
  }

  pub fn write_cold(this mut &, u8 *data, usize len) throws(std::error) -> usize
  {
    if (this._buffer.len != 0 && this._buffer.len + len < this._buffer.capacity)
    {
      this._buffer.append(data, data + len);

      this._write(&mut this._stream, this._buffer.data, this._buffer.len);
    }
    else
    {
      if (this._buffer.len != 0)
        this._write(&mut this._stream, this._buffer.data, this._buffer.len);

      this._write(&mut this._stream, data, len);
    }

    this._buffer.clear();

    return len;
  }

  pub fn putc(this mut &, u8 ch) throws(std::error) -> usize
  {
    return this.write(&ch, 1);
  }

  pub fn putc(this mut &, char ch) throws(std::error) -> usize
  {
    var buf = [0; 8];
    var len = std::ctoa(buf.data, buf.end, ch).len;

    return this.write(buf.data, len);
  }

  pub fn puts(this mut &, String &str) throws(std::error) -> usize
  {
    return this.write(str.data, str.len);
  }

  pub fn flush(this mut &) throws(std::error) -> void
  {
    if (this._buffer.len != 0)
    {
      this._write(&mut this._stream, this._buffer.data, this._buffer.len);

      this._buffer.clear();
    }

    this._flush(&mut this._stream);
  }

  std::vector<u8, Allocator, N> _buffer;

  pub ~line_writer() = default;
}
