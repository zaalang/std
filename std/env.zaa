//
// std env
//

import std.stdio : assert;
import std.iterator;
import std.string;
import std.memory;

pub struct env
{
  pub struct args
  {
    pub struct iterator
    {
      pub iterator() = default;
      pub iterator(iterator&) = default;
      pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
      pub ~iterator() = default;

      pub fn *(iterator &this) -> std::string_view
      {
        return std::string_view(*this.cursor, std::memchr(*this.cursor, 0, 4096));
      }

      pub fn ++(iterator mut &this) -> iterator mut &
      {
        ++this.cursor;
        return &this;
      }

      pub fn --(iterator mut &this) -> iterator mut &
      {
        --this.cursor;
        return &this;
      }

      pub fn ==(iterator this, iterator that) -> bool
      {
        return this.cursor == that.cursor;
      }

      pub fn -(iterator lhs, iterator rhs) -> usize
      {
        return lhs.cursor - rhs.cursor;
      }

      pub fn +(iterator lhs, usize rhs) -> iterator
      {
        return iterator(lhs.cursor + rhs);
      }

      iterator(u8 **arg)
        : cursor(arg)
      {
      }

      u8 **cursor;
    }

    pub fn len(this &) -> usize
    {
      return cast<usize>(__argc__);
    }

    pub fn begin(this &) -> iterator
    {
      return iterator(__argv__);
    }

    pub fn end(this &) -> iterator
    {
      return iterator(__argv__ + this.len);
    }

    pub fn [](this &, usize index) -> std::string_view
    {
      assert(index < this.len);

      return *(this.begin + index);
    }

    pub fn [](this &, (usize, usize) range) -> std::iterator_pair<iterator, iterator>
    {
      assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

      return std::iterator_pair(this.begin + range.0, this.begin + range.1);
    }

    pub args() = default;
    pub ~args() = default;
  }

  pub struct vars
  {
    pub struct iterator
    {
      pub iterator() = default;
      pub iterator(iterator&) = default;
      pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
      pub ~iterator() = default;

      pub fn *(iterator &this) -> std::string_view
      {
        return std::string_view(*this.cursor, std::memchr(*this.cursor, 0, 4096));
      }

      pub fn ++(iterator mut &this) -> iterator mut &
      {
        ++this.cursor;
        return &this;
      }

      pub fn ==(iterator lhs, iterator rhs) -> bool
      {
        return lhs.cursor == rhs.cursor;
      }

      pub fn ==(iterator lhs, std::end_iterator rhs) -> bool
      {
        return *lhs.cursor == null;
      }

      iterator(u8 **arg)
        : cursor(arg)
      {
      }

      u8 **cursor;
    }

    pub fn begin(this &) -> iterator
    {
      return iterator(__envp__);
    }

    pub fn end(this &) -> std::end_iterator
    {
      return std::end_iterator();
    }

    pub vars() = default;
    pub ~vars() = default;
  }
}
