//
// std env
//

import std.stdio : assert;
import std.string : String;
import std.iterator;
import std.optional;
import std.memory;

struct arg
{
  u8 *data;

  #if __cfg("os.zaos")

    u8 *end;

    fn len(this &) -> usize
    {
      return this.end - this.data;
    }

  #else

    fn len(this &) -> usize
    {
      return std::memchr(this.data, 0, 4096);
    }

  #end
}

pub struct env
{
  using std::fmt;
  using std::Range;

  pub struct Args
  {
    pub struct iterator
    {
      pub iterator() = default;
      pub iterator(iterator&) = default;
      pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
      pub ~iterator() = default;

      pub fn *(iterator &this) -> std::string_view
      {
        return std::string_view(this.cursor.data, this.cursor.len);
      }

      pub fn ++(iterator mut &this) -> iterator mut &
      {
        ++this.cursor;
        return &this;
      }

      pub fn --(iterator mut &this) -> iterator mut &
      {
        --this.cursor;
        return &this;
      }

      pub fn ==(iterator this, iterator that) -> bool
      {
        return this.cursor == that.cursor;
      }

      pub fn -(iterator lhs, iterator rhs) -> usize
      {
        return lhs.cursor - rhs.cursor;
      }

      pub fn +(iterator lhs, usize rhs) -> iterator
      {
        return iterator(lhs.cursor + rhs);
      }

      iterator(arg *arg)
        : cursor(arg)
      {
      }

      arg *cursor;
    }

    pub fn len(this &) -> usize
    {
      return cast<usize>(__argc__);
    }

    pub fn begin(this &) -> iterator
    {
      return iterator(cast<arg*>(__argv__));
    }

    pub fn end(this &) -> iterator
    {
      return iterator(cast<arg*>(__argv__) + this.len);
    }

    pub fn [](this &, usize index) -> std::string_view
    {
      assert(index < this.len);

      return *(this.begin + index);
    }

    pub fn [](this &, iterator iter) -> std::string_view
    {
      return std::string_view(iter.cursor.data, iter.cursor.len);
    }

    pub fn [](this &, (usize, usize) range)
    {
      assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

      return std::range_view(this, this.begin + range.0, this.begin + range.1);
    }

    Args() = default;
    pub Args(Args&) = default;
    pub ~Args() = default;
  }

  pub fn args() -> Args
  {
    return Args();
  }

  pub struct Vars
  {
    pub struct iterator
    {
      pub iterator() = default;
      pub iterator(iterator&) = default;
      pub fn =(iterator mut &this, iterator &that) -> iterator mut & = default;
      pub ~iterator() = default;

      pub fn *(iterator &this) -> std::string_view
      {
        return std::string_view(this.cursor.data, this.cursor.len);
      }

      pub fn ++(iterator mut &this) -> iterator mut &
      {
        ++this.cursor;
        return &this;
      }

      pub fn ==(iterator lhs, iterator rhs) -> bool
      {
        return lhs.cursor == rhs.cursor;
      }

      pub fn ==(iterator lhs, end_iterator rhs) -> bool
      {
        return lhs.cursor.data == null;
      }

      iterator(arg *arg)
        : cursor(arg)
      {
      }

      arg *cursor;
    }

    struct end_iterator
    {
      end_iterator() = default;
      end_iterator(end_iterator&) = default;
      fn =(end_iterator mut &this, end_iterator &that) -> end_iterator mut & = default;
      ~end_iterator() = default;
    }

    pub fn begin(this &) -> iterator
    {
      return iterator(cast<arg*>(__envp__));
    }

    pub fn end(this &) -> end_iterator
    {
      return end_iterator();
    }

    pub fn [](this &, iterator iter) -> std::string_view
    {
      return std::string_view(iter.cursor.data, iter.cursor.len);
    }

    Vars() = default;
    pub ~Vars() = default;
  }

  pub fn vars() -> Vars
  {
    return Vars();
  }

  pub fn var(String &key) -> std::optional<std::string_view>
  {
    for (var env = cast<arg*>(__envp__); env.data != null; ++env)
    {
      if (std::memcmp(env.data, key.data, key.len) == 0 && *(env.data + key.len) == cast('='))
        return std::string_view(env.data + key.len + 1, env.len - key.len - 1);
    }

    return None();
  }
}
