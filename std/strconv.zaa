//
// std strconv
//

import std.error;
import std.string;
import std.math;
import std.algorithm;
import std.bits.utf as utf;
import std.bits.float as flt;

pub struct strconv_result
{
  pub usize len;
  pub u8 *ptr;
  pub std::errc error;

  pub strconv_result() = default;
  pub strconv_result(strconv_result&) = default;
  pub fn =(strconv_result mut &, strconv_result&) -> strconv_result mut & = default;
  pub ~strconv_result() = default;
}

const digits = "0123456789abcdefghijklmnopqrstuvwxyz";

pub fn itoa(u8 mut *beg, u8 *end, bool value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, u8 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, u16 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, u32 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, u64 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, char value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<usize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, usize value, int base = 10) -> strconv_result
{
  std::assert(2 <= base && base < cast(digits.len));
  
  var result = strconv_result();

  var i = 0;
  let b = cast<usize>(base);

  rof (var ch = beg + (end - beg); ch == beg; --ch, ++i)
  {
    let x = value / b;

    *ch = *(digits.data + (value - x*b));

    value = x;

    if (value == 0)
      break;
  }
  
  result.ptr = end - i;
  result.len = i;
  
  if (value != 0)
  {
    result.error = std::errc::value_too_large;
  }
  
  return result;
}

pub fn itoa(u8 mut *beg, u8 *end, i8 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<isize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, i16 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<isize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, i32 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<isize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, i64 value, int base = 10) -> strconv_result
{
  return itoa(beg, end, cast<isize>(value), base);
}

pub fn itoa(u8 mut *beg, u8 *end, isize value, int base = 10) -> strconv_result
{
  var result = strconv_result();

  if (value >= 0)
  {
    result = itoa(beg, end, cast<usize>(value), base);
  }
  else if (beg != end)
  {
    result = itoa(beg + 1, end, cast<usize>(~value) + 1, base);
    
    result.ptr -= 1;
    result.len += 1;
    *(beg + (result.ptr - beg)) = cast('-');
  }
  else
  {
    result.error = std::errc::value_too_large;
  }    

  return result;
}

pub fn btoa(u8 mut *beg, u8 *end, bool value) -> strconv_result
{ 
  const true_str = "true";
  const false_str = "false";

  var result = strconv_result();

  if (value)
  {
    result.ptr = true_str.data;
    result.len = true_str.len;
  }
  else
  {
    result.ptr = false_str.data;
    result.len = false_str.len;
  }

  return result;
}

pub fn ctoa(u8 mut *beg, u8 *end, char value) -> strconv_result
{  
  var result = strconv_result();
  
  result.ptr = beg;

  if (value <= 0x7f && end - beg >= 1)
  {
    result.len = utf::encode1(beg, value);
  }
  else if (0x7f < value && value <= 0x7ff && end - beg >= 2)
  {
    result.len = utf::encode2(beg, value);
  }
  else if (0x7ff < value && value <= 0xffff && end - beg >= 3)
  {
    result.len = utf::encode3(beg, value);
  }
  else if (0xffff < value && value <= 0x10ffff && end - beg >= 4)
  {
    result.len = utf::encode4(beg, value);
  }
  else
  {
    result.error = std::errc::value_too_large;
  }

  return result;
}

fn ftoa_round(u8 mut *digits, int mut &K, usize mut &len, int prec) -> void
{
  if (prec <= 0)
  {
    *digits = (cast(-K) == len && *digits >= cast('5')) ? cast('1') : cast('0');
    len = 1;
    K = 0;
  }

  if (0 < prec && len > cast(prec))
  {
    K += cast(len) - prec;
    len = cast(prec);

    var digit = *(digits + len);

    if (digit >= cast('5'))
    {
      rof (var i = digits + len; i == digits; --i)
      {
        if ((*i)++ < cast('9'))
          break;

        *i = cast('0');

        if (i == digits)
        {
          K += 1;
          *i = cast('1');
        }
      }
    }
  }
}

fn ftoa_scientific(u8 mut *beg, u8 *end, u8 mut *digits, int K, usize len, int signbit, int prec) -> strconv_result
{
  var result = strconv_result();

  result.ptr = beg;

  var ch = beg;

  if (signbit != 0)
  {
    if (end - ch < 1)
    {
      result.error = std::errc::value_too_large;
      return result;
    }

    *ch++ = cast('-');
  }
  
  if (prec > -1)
  {
    if (prec == 0)
      prec = 1;

    ftoa_round(digits, &mut K, &mut len, prec);
  }

  if (end - ch < len + 8)
  {
    result.error = std::errc::value_too_large;
    return result;
  }

  *ch++ = *digits;
  
  if (len != 1 || prec > cast(len))
    *ch++ = cast('.');

  ch = std::copy_n(ch, end, digits + 1, len - 1);

  if (prec > cast(len))
  {
    if (cast(prec) - len > end - ch)
    {
      result.error = std::errc::value_too_large;
      return result;
    }

    ch = std::fill_n(ch, end, cast<u8>('0'), cast(prec) - len);
  }

  if (prec == 0)
    ch -= 2;

  *ch++ = cast('e');

  var exp = cast<int>(len) + K - 1;
  
  if (exp < 0)
  {
    exp = -exp;    
    *ch++ = cast('-');
  }
  else
  {
    *ch++ = cast('+');
  }
  
  var pow = 1000;
  while (exp < pow)
    pow /= 10;
  
  while (pow >= 10)
  {
    var digit = exp / pow;   
    *ch++ = cast(digit + cast('0'));
    exp -= digit * pow;
    pow /= 10;
  }

  *ch++ = cast(exp + cast('0'));
  
  result.len = ch - beg;

  return result;  
}

fn ftoa_fixed(u8 mut *beg, u8 *end, u8 mut *digits, int K, usize len, int signbit, int prec) -> strconv_result
{
  var result = strconv_result();

  result.ptr = beg;
  
  var ch = beg;

  if (signbit != 0)
  {
    if (end - ch < 1)
    {
      result.error = std::errc::value_too_large;
      return result;
    }

    *ch++ = cast('-');
  }

  if (prec > -1)
  {
    ftoa_round(digits, &mut K, &mut len, cast(len) + K + prec);
  }

  if (0 <= K)
  {
    var k = cast<usize>(K);

    if (end - ch < len + k + 2)
    {
      result.error = std::errc::value_too_large;
      return result;
    }
    
    ch = std::copy_n(ch, end, digits, len);
    ch = std::fill_n(ch, end, cast<u8>('0'), k);
    *ch++ = cast('.');
    *ch++ = cast('0');
    K = 1;
  }
  else if (len <= cast(-K))
  {
    var k = cast<usize>(-K);

    if (end - ch < k + 2)
    {
      result.error = std::errc::value_too_large;
      return result;
    }
    
    *ch++ = cast('0');
    *ch++ = cast('.');
    ch = std::fill_n(ch, end, cast<u8>('0'), k - len);
    ch = std::copy_n(ch, end, digits, len);
    K = -K;
  }
  else
  {
    var k = cast<usize>(-K);
    
    if (end - ch < len + 1)
    {
      result.error = std::errc::value_too_large;
      return result;
    }

    ch = std::copy_n(ch, end, digits, len - k);
    *ch++ = cast('.');
    ch = std::copy_n(ch, end, digits + len - k, k);
    K = -K;
  }

  if (prec > K)
  {
    if (cast(prec - K) > end - ch)
    {
      result.error = std::errc::value_too_large;
      return result;
    }

    ch = std::fill_n(ch, end, cast<u8>('0'), prec - K);
  }

  if (prec == 0)
    ch -= 2;
  
  result.len = ch - beg;

  return result;
}

fn ftoa_general(u8 mut *beg, u8 *end, u8 mut *digits, int K, usize len, int signbit, int prec) -> strconv_result
{
  var exp = cast<int>(len) + K - 1;

  if (exp < -4 || exp >= 16)
    return ftoa_scientific(beg, end, digits, K, len, signbit, prec);
  else
    return ftoa_fixed(beg, end, digits, K, len, signbit, prec >= 0 ? std::max(prec - exp - 1, 1) : prec);
}

fn ftoa_special<T>(u8 mut *beg, u8 *end, T value) -> strconv_result
{
  const nan_str = "-NaN";
  const inf_str = "-Inf";

  var result = strconv_result();

  if (std::is_infinite(value))
  {
    result.ptr = inf_str.data;
    result.len = inf_str.len;
  }
  else
  {
    result.ptr = nan_str.data;
    result.len = nan_str.len;
  }

  if (std::signbit(value) == 0)
  {
    result.ptr += 1;
    result.len -= 1;
  }

  return result;
}

pub fn ftoa(u8 mut *beg, u8 *end, f32 value, char fmt = ' ', int prec = -1) -> strconv_result
{
  if (!std::is_finite(value))
    return ftoa_special(beg, end, value);

  var K = 0;
  var len = 0;
  var digits = [0; 32];

  flt::grisu(digits.begin, digits.end, value, &mut K, &mut len);

  switch (fmt)
  {
    case 'f':
      return ftoa_fixed(beg, end, digits.data, K, len, std::signbit(value), prec);

    case 'e':
      return ftoa_scientific(beg, end, digits.data, K, len, std::signbit(value), prec);
  }

  return ftoa_general(beg, end, digits.data, K, len, std::signbit(value), prec);
}

pub fn ftoa(u8 mut *beg, u8 *end, f64 value, char fmt = ' ', int prec = -1) -> strconv_result
{
  if (!std::is_finite(value))
    return ftoa_special(beg, end, value);

  var K = 0;
  var len = 0;
  var digits = [0; 32];

  flt::grisu(digits.begin, digits.end, value, &mut K, &mut len);

  switch (fmt)
  {
    case 'f':
      return ftoa_fixed(beg, end, digits.data, K, len, std::signbit(value), prec);

    case 'e':
      return ftoa_scientific(beg, end, digits.data, K, len, std::signbit(value), prec);
  }

  return ftoa_general(beg, end, digits.data, K, len, std::signbit(value), prec);
}

pub fn atoi(u8 *beg, u8 *end, bool mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v <= 1)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, u8 mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v <= 255)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, u16 mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v <= 65_535)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, u32 mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v <= 4_294_967_295)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, u64 mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    value = cast(v);
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, char mut &value, int base = 10) -> strconv_result
{
  var v = usize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v <= 4_294_967_295)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, usize mut &value, int base = 10) -> strconv_result
{
  std::assert(2 <= base && base < cast(digits.len));

  var result = strconv_result();

  var v = 0;
  var ch = beg;

  for (; ch != end; ++ch)
  {
    var d = 0;

    if (*ch == cast('_'))
      continue;

    if (cast('0') <= *ch && *ch <= cast('9'))
      d = *ch - cast('0');
    else if (cast('a') <= *ch && *ch <= cast('z'))
      d = 10 + *ch - cast('a');
    else if (cast('A') <= *ch && *ch <= cast('Z'))
      d = 10 + *ch - cast('A');
    else
      break;

    if (d >= cast(base))
      break;

    var sum = std::muladd_with_carry(v, cast(base), cast(d));

    if (sum.1 != 0)
      result.error = std::errc::result_out_of_range;

    v = sum.0;
  }

  if (ch == beg)
    result.error = std::errc::invalid_argument;

  if (!result.error)
    value = v;

  result.ptr = beg;
  result.len = ch - beg;

  return result;
}

pub fn atoi(u8 *beg, u8 *end, i8 mut &value, int base = 10) -> strconv_result
{
  var v = isize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v >= -128 && v <= 127)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, i16 mut &value, int base = 10) -> strconv_result
{
  var v = isize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v >= -32_768 && v <= 32_767)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, i32 mut &value, int base = 10) -> strconv_result
{
  var v = isize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    if (v >= -2_147_483_648 && v <= 2_147_483_647)
      value = cast(v);
    else
      result.error = std::errc::result_out_of_range;
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, i64 mut &value, int base = 10) -> strconv_result
{
  var v = isize(0);
  var result = atoi(beg, end, &mut v, base);

  if (!result.error)
  {
    value = cast(v);
  }

  return result;
}

pub fn atoi(u8 *beg, u8 *end, isize mut &value, int base = 10) -> strconv_result
{
  var ch = beg;
  var body = usize(0);

  if (ch != end && *ch == cast('-'))
    ++ch;

  var result = atoi(ch, end, &mut body, base);

  if (!result.error)
  {
    if (ch != beg) // negative
    {
      if (body == 0)
        value = 0;
      else if (body <= 9_223_372_036_854_775_808)
        value = ~cast<isize>(body - 1);
      else
        result.error = std::errc::result_out_of_range;
    }
    else
    {
      if (body <= 9_223_372_036_854_775_807)
        value = cast<isize>(body);
      else
        result.error = std::errc::result_out_of_range;
    }
  }

  if (ch != beg) // negative
  {
    result.ptr -= 1;
    result.len += 1;
  }

  return result;
}

fn atof_special<T>(u8 *beg, u8 *end, T mut &value) -> usize
{
  const inf_strs = [ "infinity", "INFINITY", "inf", "Inf", "INF" ];
  const nan_strs = [ "NaN", "NAN", "nan" ];

  for (var &str : inf_strs)
  {
    if (end - beg < str.len)
      continue;

    if (std::span(beg, str.len) == str)
    {
      value = __inf();
      return str.len;
    }
  }

  for (var &str : nan_strs)
  {
    if (end - beg < str.len)
      break;

    if (std::span(beg, str.len) == str)
    {
      value = __nan();
      return str.len;
    }
  }

  return 0;
}

pub fn atof(u8 *beg, u8 *end, f32 mut &value) -> strconv_result
{
  var result = strconv_result();

  var ch = beg;
  if (ch != end && *ch == cast('-'))
    ++ch;

  var len = atof_special(ch, end, &mut value);

  if (len == 0)
  {
    len = flt::scan(ch, end, &mut value);

    if (len == 0)
      result.error = std::errc::invalid_argument;
  }

  if (ch != beg) // negative
  {
    len += 1;

    if (!result.error)
      value = -value;
  }

  result.ptr = beg;
  result.len = len;

  return result;
}

pub fn atof(u8 *beg, u8 *end, f64 mut &value) -> strconv_result
{
  var result = strconv_result();

  var ch = beg;
  if (ch != end && *ch == cast('-'))
    ++ch;

  var len = atof_special(ch, end, &mut value);

  if (len == 0)
  {
    len = flt::scan(ch, end, &mut value);

    if (len == 0)
      result.error = std::errc::invalid_argument;
  }

  if (ch != beg) // negative
  {
    len += 1;
    value = -value;
  }

  result.ptr = beg;
  result.len = len;

  return result;
}

pub fn atob(u8 *beg, u8 *end, bool mut &value) -> strconv_result
{
  const true_strs = [ "1", "true", "True", "TRUE" ];
  const false_strs = [ "0", "false", "False", "FALSE" ];

  var result = strconv_result();

  result.ptr = beg;

  for (var &str : true_strs)
  {
    if (end - beg < str.len)
      break;

    if (std::span(beg, str.len) == str)
    {
      value = true;
      result.len = str.len;
      return result;
    }
  }

  for (var &str : false_strs)
  {
    if (end - beg < str.len)
      break;

    if (std::span(beg, str.len) == str)
    {
      value = false;
      result.len = str.len;
      return result;
    }
  }

  result.error = std::errc::invalid_argument;

  return result;
}

pub fn atoc(u8 *beg, u8 *end, char mut &value) -> strconv_result
{   
  var result = strconv_result();
  
  var v = 0;
  var ch = beg;
  var state = 0;

  for (; ch != end; )
  {
    if (utf::decode(&mut state, &mut v, *ch) != utf::REJECT)
      ++ch;
    
    if (state == utf::ACCEPT || state == utf::REJECT)
      break;
  }

  if (ch == beg || state != 0)
    result.error = std::errc::invalid_argument;

  if (!result.error)
    value = v;

  result.ptr = beg;
  result.len = ch - beg;

  return result;
}
