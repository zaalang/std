//
// std algorithm partition
//

import std.algorithm;
import std.range : Iterator, BidirectionalIterator, RandomAccessIterator, Sentinal, Range;

#[lifetime(repose(first, range), repose(last, range))]
pub fn is_partitioned<T, I, Pred>(Range<T> &range, I first, Sentinal<I> last, Pred &pred) -> bool
{
  first = std::find_if_not(range, first, last, pred);

  for (; first != last; ++first)
    if (pred(range[first]))
      return false;

  return true;
}

pub fn is_partitioned<T, Pred>(Range<T> &range, Pred &pred) -> bool
{
  return is_partitioned(range, range.begin, range.end, pred);
}

#[lifetime(repose(first, range), repose(last, range), depend(range))]
pub fn partition<T, I, Pred>(Range<T> mut &range, I first, Sentinal<I> last, Pred &pred) -> I
{
  first = std::find_if_not(range, first, last, pred);

  if (first == last)
    return first;

  for (var i = std::next(first); i != last; ++i)
  {
    if (pred(range[i]))
    {
      std::swap(&mut range[i], &mut range[first]);

      ++first;
    }
  }

  return first;
}

#[lifetime(depend(range))]
pub fn partition<T, Pred>(Range<T> mut &range, Pred &pred)
{
  return partition(&mut range, range.begin, range.end, pred);
}

#[lifetime(repose(first, range), repose(last, range), depend(range))]
pub fn stable_partition<T, I, Pred>(Range<T> mut &range, I first, Sentinal<I> last, Pred &pred) -> I
{
  first = std::find_if_not(range, first, last, pred);

  for (var it = first; it != last; )
  {
    for (; it != last && !pred(range[it]); ++it)
      ;

    if (it == last)
      break;

    var middle = it;

    for (; it != last && pred(range[it]); ++it)
      ;

    std::rotate(&mut range, first, middle, it);

    first = std::next(first, std::distance(middle, it));
  }

  return first;
}

#[lifetime(depend(range))]
pub fn stable_partition<T, Pred>(Range<T> mut &range, Pred &pred)
{
  return stable_partition(&mut range, range.begin, range.end, pred);
}

#[lifetime(repose(first, range), repose(last, range), depend(range))]
pub fn partition_point<T, I, Pred>(Range<T> &range, I first, Sentinal<I> last, Pred &pred) -> I
{
  #if (requires (I x, usize y) { x + y; })
  {
    for (var len = std::distance(first, last); len > 0; )
    {
      var m = len / 2;
      var it = first + m;

      if (pred(range[it]))
      {
        first = ++it;
        len -= m + 1;
      }
      else
      {
        len = m;
      }
    }
  }
  #else
  {
    for (; first != last; ++first)
    {
      if (!pred(range[first]))
        break;
    }
  }

  return first;
}

#[lifetime(depend(range))]
pub fn partition_point<T, Pred>(Range<T> &range, Pred &pred)
{
  return partition_point(range, range.begin, range.end, pred);
}
