//
// std error
//

import std.string;

pub vtable error_category
{
  fn message(uintptr value, u8 mut *buffer, usize buflen) -> std::string_view;

  //fn ref(uintptr value) -> void;
  //fn unref(uintptr value) -> void;
}

pub struct source_location
{
  pub source_location(source_location &that) = default;
  pub fn =(source_location mut &, source_location &) -> source_location mut & = default;
  pub ~source_location() = default;

  pub source_location((std::string_literal, int, int, std::string_literal) &site)
    : lineno(site.1),
      file_name(site.0),
      function_name(site.3)
  {
  }

  pub int lineno;
  pub std::string_literal file_name;
  pub std::string_literal function_name;
}

pub struct error
{
  pub uintptr value;
  pub error_category *category;
  // pub source_location location; // ?
  // pub backtrace // ?

  pub fn message(this &, u8 mut *buffer, usize buflen)
  {
    return this.category.message(this.value, buffer, buflen);
  }

  pub error(error &) = default;
  pub fn =(error mut &, error &) -> error mut & = default;
  pub ~error() = default;
}

pub enum errc : u32
{
  not_permitted = 1,
  file_not_found = 2,
  interrupted = 4,
  io_error = 5,
  no_such_device = 6,
  format_error = 8,
  bad_descriptor = 9,
  would_block = 11,
  permission_denied = 13,
  bad_address = 14,
  device_busy = 16,
  already_exists = 17,
  not_a_directory = 20,
  invalid_argument = 22,
  resource_exhausted = 24,
  file_too_large = 27,
  no_space_remaining = 28,
  readonly_file_system = 30,
  argument_out_of_domain = 33,
  result_out_of_range = 34,
  name_too_long = 36,
  not_implemented = 38,
  directory_not_empty = 39,
  value_too_large = 75,
  not_supported = 95,
  timed_out = 110,
  operation_cancelled = 125,
  overflow = 139,
  line_too_long = 175,

  pub const fn bool(errc code) -> bool
  {
    return cast<u32>(code) != 0;
  }
}

pub struct system_error_category : error_category
{
  fn instance() -> system_error_category *
  {
    static instance = #system_error_category::system_error_category();

    return &instance;
  }

  pub fn message(uintptr value, u8 mut *buffer, usize buflen) -> std::string_view
  {
    return std::meta::enumerator_name(cast<errc>(value));
  }

  system_error_category()
    : super(error_category<system_error_category>)
  {
  }

  system_error_category(system_error_category &) = default;
  ~system_error_category() = default;
}

pub fn system_error_category() -> system_error_category *
{
  return system_error_category::instance;
}

pub fn system_error(errc code) -> error
{
  var result = error(void);

  result.value = cast(code);
  result.category = system_error_category::instance;

  return result;
}

pub fn ==(error &lhs, error &rhs) -> bool
{
  return lhs.category == rhs.category && lhs.value == rhs.value;
}

pub fn ==(error &lhs, errc rhs) -> bool
{
  return lhs.category == system_error_category::instance && lhs.value == cast(rhs);
}
