//
// std string
//

import std.span;
import std.stdlib : assert;
import std.algorithm : swap;
import std.memory : memcpy, memmove, memchr, memrchr, memcmp;
import std.unicode : is_whitespace;
import std.range: Range;
import std.fmt;
import std.bits.utf as utf;

pub concept String
{
  requires<T> (T x) { x.len; };
  requires<T> (T x) { x.data; };
  requires<T> (T x) -> u8 { return *x.data; };
  requires<T> (T x) { x.begin; };
  requires<T> (T x) { x.end; };
}

pub using string_literal = typeof("");

pub struct string_view : std::span<u8>
{
  pub string_view(u8 *data, usize len)
    : super(data, len)
  {
  }

  pub string_view(u8 *beg, u8 *end)
    : super(beg, end)
  {
  }

  pub string_view(string_literal &str)
    : super(str.data, str.len)
  {
  }

  pub fn [](this&, (usize, usize) range) -> string_view
  {
    assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

    return string_view(this.data + range.0, this.data + range.1);
  }

  pub struct char_iterator : u8 *
  {
    pub char_iterator(char_iterator&) = default;
    pub ~char_iterator() = default;

    pub fn *(char_iterator &this) -> char
    {
      return this.codepoint;
    }

    pub fn ++(char_iterator mut &this) -> char_iterator mut &
    {
      this.super += this.n;

      this.n = 0;

      var state = 0;
      for(var ch = this.super; ch != this.end; ++ch)
      {
        this.n += 1;

        if (utf::decode(state, this.codepoint, *ch) == 0)
          break;
      }

      return &this;
    }

    pub fn --(char_iterator mut &this) -> char_iterator mut &
    {
      this.n = utf::len_last(this.beg, this.super);

      var state = 0;
      for(var ch = this.super - this.n; ch != this.super; ++ch)
      {
        utf::decode(state, this.codepoint, *ch);
      }

      this.super -= this.n;

      return &this;
    }

    pub fn -(u8 *rhs, char_iterator &lhs) -> usize = delete;

    pub fn ==(char_iterator &lhs, end_iterator rhs) -> bool
    {
      return lhs.n == 0;
    }

    pub fn len(char_iterator &this) -> usize
    {
      return this.n;
    }

    char_iterator(u8 *beg, u8 *end, u8 *cursor)
      : super(cursor), beg(beg), end(end)
    {
    }

    usize n;
    char codepoint;
    u8 *beg, u8 *end
  }

  pub struct line_iterator : u8 *
  {
    pub line_iterator(line_iterator&) = default;
    pub ~line_iterator() = default;

    pub fn *(line_iterator &this) -> string_view
    {
      return string_view(this.super, this.m);
    }

    pub fn ++(line_iterator mut &this) -> line_iterator mut &
    {
      this.super += this.n;

      if (this.super != this.end)
      {
        this.n = memchr(this.super, cast('\n'), this.end - this.super);

        this.m = this.n;

        if (this.super + this.n != this.end)
        {
          if (this.m != 0 && *(this.super + this.m - 1) == cast('\r'))
            this.m -= 1;

          this.n += 1;
        }
      }
      else
      {
        this.n = this.m = 0;
      }

      return &this;
    }

    pub fn -(u8 *rhs, char_iterator &lhs) -> usize = delete;

    pub fn ==(line_iterator &lhs, end_iterator rhs) -> bool
    {
      return lhs.n == 0;
    }

    line_iterator(u8 *beg, u8 *end)
      : super(beg), end(end)
    {
    }

    usize n, usize m;
    u8 *end;
  }

  pub struct word_iterator : u8 *
  {
    pub word_iterator(word_iterator&) = default;
    pub ~word_iterator() = default;

    pub fn *(word_iterator &this) -> string_view
    {
      return string_view(this.super, this.n);
    }

    pub fn ++(word_iterator mut &this) -> word_iterator mut &
    {
      this.super += this.n;
      this.super = find_first_not_of(string_view(this.super, this.end), this.delimiters);

      if (this.super != this.end)
      {
        this.n = find_first_of(string_view(this.super + 1, this.end), this.delimiters) - this.super;
      }
      else
      {
        this.n = 0;
      }

      return &this;
    }

    pub fn -(u8 *rhs, word_iterator &lhs) -> usize = delete;

    pub fn ==(word_iterator &lhs, end_iterator rhs) -> bool
    {
      return lhs.n == 0;
    }

    word_iterator(u8 *beg, u8 *end, String &delimiters)
      : super(beg), end(end), delimiters(delimiters.data, delimiters.len)
    {
    }

    usize n;
    string_view delimiters;
    u8 *end;
  }

  pub struct split_iterator : u8 *
  {
    pub split_iterator(split_iterator&) = default;
    pub ~split_iterator() = default;

    pub fn *(split_iterator &this) -> string_view
    {
      return string_view(this.super, this.m);
    }

    pub fn ++(split_iterator mut &this) -> split_iterator mut &
    {
      this.super += this.n;

      if (this.count > 2)
      {
        var it = find_first(string_view(this.super, this.end), this.seperator);

        this.n = this.m = it - this.super;

        if (it != this.end)
        {
          this.n += this.seperator.len;
        }
        else
        {
          this.count = 2;
        }
      }
      else
      {
        this.n = this.m = this.end - this.super;
      }

      if (this.inclusive)
      {
        this.m = this.n;

        if (this.n == 0)
          this.count = 1;
      }

      this.count -= 1;

      return &this;
    }

    pub fn -(u8 *rhs, char_iterator &lhs) -> usize = delete;

    pub fn ==(split_iterator &lhs, end_iterator rhs) -> bool
    {
      return lhs.count == 0;//lhs.n + lhs.leftover == 0;
    }

    split_iterator(u8 *beg, u8 *end, String &seperator, bool inclusive, usize count)
      : super(beg), end(end), seperator(seperator.data, seperator.len), inclusive(inclusive), count(count)
    {
    }

    usize count;
    bool inclusive;
    usize n, usize m;
    string_view seperator;
    u8 *end;
  }

  struct end_iterator
  {
    end_iterator() = default;
    end_iterator(end_iterator&) = default;
    fn =(end_iterator mut &this, end_iterator &that) -> end_iterator mut & = default;
    ~end_iterator() = default;
  }

  pub fn chars(this &) -> std::iterator_pair<char_iterator, char_iterator>
  {
    return std::iterator_pair(++char_iterator(this.begin, this.end, this.begin), char_iterator(this.begin, this.end, this.end));
  }

  pub fn lines(this &) -> std::iterator_pair<line_iterator, end_iterator>
  {
    return std::iterator_pair(++line_iterator(this.begin, this.end), end_iterator);
  }

  pub fn words(this &, String &delimiters = " \r\n\t") -> std::iterator_pair<word_iterator, end_iterator>
  {
    return std::iterator_pair(++word_iterator(this.begin, this.end, delimiters), end_iterator);
  }

  pub fn split(this &, String &seperator) -> std::iterator_pair<split_iterator, end_iterator>
  {
    return std::iterator_pair(++split_iterator(this.begin, this.end, seperator, false, ~usize(0)), end_iterator);
  }

  pub fn split_after(this &, String &seperator) -> std::iterator_pair<split_iterator, end_iterator>
  {
    return std::iterator_pair(++split_iterator(this.begin, this.end, seperator, true, ~usize(0)), end_iterator);
  }

  pub fn split_n(this &, usize n, String &seperator) -> std::iterator_pair<split_iterator, end_iterator>
  {
    return std::iterator_pair(++split_iterator(this.begin, this.end, seperator, false, n + 1), end_iterator);
  }

  pub fn split_after_n(this &, usize n, String &seperator) -> std::iterator_pair<split_iterator, end_iterator>
  {
    return std::iterator_pair(++split_iterator(this.begin, this.end, seperator, true, n + 1), end_iterator);
  }

  pub string_view(string_view &that) = default;
  pub ~string_view() = default;
}

pub fn String(string_literal &str) -> string_view
{
  return string_view(str.data, str.len);
}

pub struct string<Allocator = std::allocator, N = 24>
{
  pub string(Allocator &allocator = Allocator())
    : _allocator(allocator),
      _smallbuffer(void)
  {
    init(this);
  }
  
  pub string(String &&other, Allocator &allocator = Allocator())
    : _allocator(allocator),
      _smallbuffer(void)
  {
    init(this);

    #if (__is_rvalue<typeof(other)> && requires (typeof(this) this, typeof(other) other) { take(this, other); })
      this.take(other);
    #else
      this.write(other.data, other.len);
  }

  pub fn =(this mut &, String &&other) -> string mut &
  {
    if (this._data <= other.data && other.data < this._data + this._len)
    {
      memmove(this._data, other.data, other.len);
      this._len = other.len;
      return &this;
    }

    clear(this);

    #if (__is_rvalue<typeof(other)> && requires (typeof(this) this, typeof(other) other) { take(this, other); })
      this.take(other);
    #else
      this.write(other.data, other.len);

    return &this;
  }

  pub fn with_capacity(usize capacity, Allocator &allocator = Allocator()) -> string
  {
    var result = string(allocator);

    result.reserve(capacity);

    return result;
  }

  pub fn from_utf8(u8 *data, usize len, Allocator &allocator = Allocator()) -> string
  {
    return string(string_view(data, len), allocator);
  }

  pub fn from_utf8(u8 *beg, u8 *end, Allocator &allocator = Allocator()) -> string
  {
    return string(string_view(beg, end), allocator);
  }

  pub ~string()
  {
    this._len = 0;

    if (_data != _smallbuffer.data)
      this._allocator.free(_data, _capacity);
  }
  
  pub fn len(this &) -> usize
  {
    return this._len;
  }
  
  pub fn empty(this &) -> bool
  {
    return this._len == 0;
  }
  
  pub fn capacity(this &) -> usize
  {
    return this._capacity;
  }

  pub fn data(this &&)
  {
    return this._data;
  }

  pub fn allocator(this &) -> Allocator &
  {
    return &this._allocator;
  }

  pub fn [](this &&, usize index)
  {
    assert(index < this.len);

    return &*(this.data + index);
  }

  pub fn [](this &, (usize, usize) range) -> string_view
  {
    assert(range.0 <= range.1 && range.0 <= this.len && range.1 <= this.len);

    return string_view(this.data + range.0, this.data + range.1);
  }

  pub fn begin(this &&)
  {
    return this._data;
  }

  pub fn end(this &&)
  {
    return this._data + this._len;
  }

  pub fn reserve(this mut &, usize capacity) -> void
  {
    if (this._capacity < capacity)
    {
      var data = cast<u8 mut *>(this._allocator.allocate(capacity, alignof<u8>));
      
      memcpy(data, this._data, this._len);
      swap(this._data, data);
      swap(this._capacity, capacity);

      if (data != this._smallbuffer.data)
        this._allocator.free(data, capacity);
    }
  }

  pub fn resize(this mut &, usize size) -> void
  {
    reserve(this, size);

    this._len = size;
  }

  pub fn clear(this mut &) -> void
  {
    this._len = 0;
  }

  pub fn append(this mut &, var & ...args) -> usize
  {
    try
    {
      return std::print_to(this, args...);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  pub fn appendf(this mut &, String &format, var & ...args) -> usize
  {
    ensure_capacity(this, this._len + format.len);

    try
    {
      return std::format_to(this, format, args...);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  pub fn appendf(this mut &, #string_literal format, var & ...args) -> usize
  {
    ensure_capacity(this, this._len + format.len);

    try
    {
      return std::format_to(this, format, args...);
    }
    catch(std::error e)
    {
      return 0;
    }
  }

  pub fn write(this mut &, u8 *data, usize len) -> usize
  {
    ensure_capacity(this, this._len + len);

    memcpy(this._data + this._len, data, len);

    this._len += len;

    return len;
  }

  fn init(this mut &)
  {
    this._len = 0;
    this._data = this._smallbuffer.data;
    this._capacity = this._smallbuffer.len;
  } 

  fn ensure_capacity(this mut &, usize len) -> void
  {
    if (len <= this._capacity)
      return;

    if (len < 2*this._capacity)
      len = 2*this._capacity;

    reserve(this, len);
  }

  fn take<M>(this mut &, string<Allocator, M> mut &other) -> void
  {
    assert(this.empty);
    assert(this.allocator == other.allocator);

    if (other._data != other._smallbuffer.data)
    {
      swap(this._data, other._data);
      swap(this._len, other._len);
      swap(this._capacity, other._capacity);

      if (other._data == this._smallbuffer.data)
        other.init();
    }
    else
    {
      write(this, other.data, other.len);

      other.clear();
    }
  }

  usize _len;
  u8 mut *_data;

  usize _capacity;
  u8[N] _smallbuffer;

  Allocator _allocator;
}

pub fn sprint(string mut &str, var & ...args) -> usize
{
  str.clear();

  return str.append(args...);
}

pub fn sprintf(string mut &str, String &format, var & ...args) -> usize
{
  str.clear();

  return str.appendf(format, args...);
}

pub fn sprintf(string mut &str, #string_literal format, var & ...args) -> usize
{
  str.clear();

  return str.appendf(format, args...);
}

pub fn chars(String &str)
{
  return string_view(str.data, str.len).chars;
}

pub fn lines(String &str)
{
  return string_view(str.data, str.len).lines;
}

pub fn words(String &str, String &delimiters = " \r\n\t")
{
  return string_view(str.data, str.len).words(delimiters);
}

pub fn split(String &str, String &seperator)
{
  return string_view(str.data, str.len).split(seperator);
}

pub fn split_after(String &str, String &seperator)
{
  return string_view(str.data, str.len).split_after(seperator);
}

pub fn split_n(String &str, usize n, String &seperator)
{
  return string_view(str.data, str.len).split_n(n, seperator);
}

pub fn split_after_n(String &str, usize n, String &seperator)
{
  return string_view(str.data, str.len).split_after_n(n, seperator);
}

pub fn find_first(String &&haystack, char needle)
{
  return find_first(&&haystack, haystack.begin, needle);
}

pub fn find_first(String &&haystack, u8 *pos, char needle)
{
  var n = pos - haystack.data;

  if (needle < 0x80)
    return haystack.data + n + memchr(haystack.data + n, cast<u8>(needle), haystack.len - n);

  var buf = [0; 8];
  var len = utf::encode(buf.data, needle);

  return find_first(&&haystack, pos, string_view(buf.data, len));
}

pub fn find_first(String &&haystack, String &needle)
{
  return find_first(&&haystack, haystack.begin, needle);
}

pub fn find_first(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  if (needle.len == 0)
    return haystack.data + n;

  if (needle.len <= haystack.len - n)
  {
    for(var i = haystack.data + n, var end = haystack.end + 1 - needle.len; i != end; ++i)
    {
      if (memcmp(i, needle.data, needle.len) == 0)
        return i;
    }
  }

  return haystack.end;
}

pub fn find_first_of(String &&haystack, String &needle)
{
  return find_first_of(&&haystack, haystack.begin, needle);
}

pub fn find_first_of(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  for(var i = haystack.data + n, var end = haystack.end; i != end; ++i)
  {
    for(var j = needle.begin; j != needle.end; )
    {
      var k = utf::len_next(j, needle.end);

      if (memcmp(i, j, k) == 0)
        return i;

      j += k;
    }
  }

  return haystack.end;
}

pub fn find_first_not_of(String &&haystack, String &needle)
{
  return find_first_not_of(&&haystack, haystack.begin, needle);
}

pub fn find_first_not_of(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  for(var i = haystack.data + n, var end = haystack.end; i != end; ++i)
  {
    var found = true;
    for(var j = needle.begin; j != needle.end; )
    {
      var k = utf::len_next(j, needle.end);

      if (memcmp(i, j, k) == 0)
      {
        found = false;
        break;
      }

      j += k;
    }

    if (found)
      return i;
  }

  return haystack.end;
}

pub fn find_last(String &&haystack, char needle)
{
  return find_last(&&haystack, haystack.end, needle);
}

pub fn find_last(String &&haystack, u8 *pos, char needle)
{
  var n = pos - haystack.data;

  if (needle < 0x80)
    return haystack.data + memrchr(haystack.data, cast<u8>(needle), n);

  var buf = [0; 8];
  var len = utf::encode(buf.data, needle);

  return find_last(&&haystack, pos, string_view(buf.data, len));
}

pub fn find_last(String &&haystack, String &needle)
{
  return find_last(&&haystack, haystack.end, needle);
}

pub fn find_last(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  if (needle.len == 0)
    return haystack.data + n;

  if (needle.len <= n)
  {
    rof(var i = haystack.data + n + 1 - needle.len, var end = haystack.data; i == end; --i)
    {
      if (memcmp(i, needle.data, needle.len) == 0)
        return i;
    }
  }

  return haystack.end;
}

pub fn find_last_of(String &&haystack, String &needle)
{
  return find_last_of(&&haystack, haystack.end, needle);
}

pub fn find_last_of(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  rof(var i = haystack.data + n, var end = haystack.data; i == end; --i)
  {
    for(var j = needle.begin; j != needle.end; )
    {
      var k = utf::len_next(j, needle.end);

      if (memcmp(i, j, k) == 0)
        return i;

      j += k;
    }
  }

  return haystack.end;
}

pub fn find_last_not_of(String &&haystack, String &needle)
{
  return find_last_not_of(&&haystack, haystack.end, needle);
}

pub fn find_last_not_of(String &&haystack, u8 *pos, String &needle)
{
  var n = pos - haystack.data;

  rof(var i = haystack.data + n, var end = haystack.data; i == end; --i)
  {
    var found = true;
    for(var j = needle.begin; j != needle.end; )
    {
      var k = utf::len_next(j, needle.end);

      if (memcmp(i, j, k) == 0)
      {
        found = false;
        break;
      }

      j += k;
    }

    if (found)
      return i;
  }

  return haystack.end;
}

pub fn trim_left(String &str) -> string_view
{
  var i = str.chars.begin;

  for(; i != str.end; ++i)
  {
    if (!is_whitespace(*i))
      break;
  }

  return string_view(i, str.end);
}

pub fn trim_right(String &str) -> string_view
{
  var j = str.end;

  rof(var i = str.chars.end; i == str.begin; --i)
  {
    if (!is_whitespace(*i))
      break;

    j = i;
  }

  return string_view(str.begin, j);
}

pub fn trim(String &str) -> string_view
{
  return trim_right(trim_left(str));
}
